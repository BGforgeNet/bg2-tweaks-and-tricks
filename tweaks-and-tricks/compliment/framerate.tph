INCLUDE ~%MOD_FOLDER%/compliment/sound_list.tph~ // define a sound_list array with compliments and insults

//get framerate
COPY ~baldur.ini~ ~baldur.ini~
  SPRINT string ~~
  offset = 0
  WHILE !(~Rate=~ STRING_EQUAL ~%string%~) BEGIN
    READ_ASCII offset string (5)
    SET offset = (offset + 1)
  END
  offset = (offset + 4)
  READ_ASCII offset framerate (2)
  PATCH_PRINT ~Current framerate is: %framerate%~
BUT_ONLY

//Copy wavc sounds that we need to adjust
ACTION_PHP_EACH sound_list AS index => sound BEGIN
  COPY_EXISTING ~%sound%.wav~ ~override~
END

//copy converters to override
AT_NOW ~%MOD_FOLDER%/compliment/bat/g_copy.bat~

//decompress to WAV
AT_NOW ~%MOD_FOLDER%/compliment/bat/g_decode.bat~
//adjust WAV
ACTION_PHP_EACH sound_list AS index => sound BEGIN
  COPY_EXISTING ~%sound%.wav~ ~override~
    filesize = (SOURCE_SIZE - 8) //without header
    datasize = (SOURCE_SIZE - 44) //actual data size
    //lengthen it
    delta = (datasize*framerate/30 - datasize) //BG1 original frame rate is 30
    WRITE_LONG 0x4 (filesize + delta)
    WRITE_LONG 0x2a (datasize + delta)
    INSERT_BYTES SOURCE_SIZE delta
  BUT_ONLY
END

//cleaning remains
AT_NOW ~%MOD_FOLDER%/compliment/bat/g_delete.bat~
AT_UNINSTALL ~%MOD_FOLDER%/compliment/bat/g_delete.bat~

PRINT "Sounds are adjusted to %framerate% FPS"
