OUTER_SPRINT ~G_BLANK~ ~%TAB%%MNL%%LNL% ~ //2-line replacement for bard notifications

// Ranger
COPY_EXISTING //speed up Delon and Mairyn
  ~delon.cre~    ~override~
  ~rspirit1.cre~ ~override~
  ADD_CRE_ITEM ~boot01~ #0 #0 #0 ~unstealable&undroppable~ ~boots~
BUT_ONLY
COPY_EXISTING ~ar1100.bcs~ ~override~ //create Mairyn next to the player
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE
      ~CreateCreatureObjectOffScreen("rspirit1",Player1,0,0,0)~
      ~CreateCreatureObject("rspirit1",Player1,0,0,0)~
  END
BUT_ONLY
COPY_EXISTING ~baldur.bcs~ ~override~ //create Delon next to the player
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE
      ~CreateCreatureObjectOffScreen("delon",Player1,0,0,0)~
      ~CreateCreatureObject("delon",Player1,0,0,0)~
  END
BUT_ONLY
COPY_EXISTING ~minsc.bcs~ ~override~ //create Delon next to Minsc
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE
      ~CreateCreatureObjectOffScreen("delon",Myself,0,0,0)~
      ~CreateCreatureObject("delon",Myself,0,0,0)~
  END
BUT_ONLY
COPY_EXISTING ~delon.bcs~ ~override~ //Delon talks to Minsc from distance
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE
      ~Dialogue("Minsc")~
      ~StartDialogNoSet("Minsc")~
  END
BUT_ONLY

<<<<<<<< .../tnt-inlined/improved_strongholds/ranger.baf
IF
  Global("RangerProtector","GLOBAL",1)
  GlobalTimerExpired("MairynAppear","GLOBAL")
//  Global("MairynSpawn","AR1100",0)
  Global("g_tnt_ranger_notif","GLOBAL",0)
  !Global("Chapter","GLOBAL",4)
  !Global("Chapter","GLOBAL",5)
  !Global("Chapter","GLOBAL",7)
THEN
  RESPONSE #100
    SetGlobal("g_tnt_ranger_notif","GLOBAL",1)
    DisplayString(Player1,@139) //It's time to visit the cabin.
END
>>>>>>>>
EXTEND_BOTTOM ~baldur.bcs~ ~.../tnt-inlined/improved_strongholds/ranger.baf~ EVAL



//Mage
<<<<<<<< .../tnt-inlined/improved_strongholds/mage.baf
IF
  GlobalTimerNotExpired("ApprentiBuildTimer","GLOBAL")
  Global("g_tnt_mage_notif","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("g_tnt_mage_notif","GLOBAL",0)
END
IF
  !Global("ApprentiOver","GLOBAL",1)
  GlobalTimerExpired("ApprentiBuildTimer","GLOBAL")
  Global("g_tnt_mage_notif","GLOBAL",0)
  !Global("Chapter","GLOBAL",4)
  !Global("Chapter","GLOBAL",5)
  !Global("Chapter","GLOBAL",7)
THEN
  RESPONSE #100
    SetGlobal("g_tnt_mage_notif","GLOBAL",1)
    DisplayString(Player1,@137) //The apprentices must have finished the task. Time to check up on them.
END
>>>>>>>>
EXTEND_BOTTOM ~baldur.bcs~ ~.../tnt-inlined/improved_strongholds/mage.baf~ EVAL


// Bard
COPY_EXISTING  //speed up Meck
  ~bdgoph01.cre~ ~override~
  ~bdgoph02.cre~ ~override~
  ADD_CRE_ITEM ~boot01~ #0 #0 #0 ~unstealable&undroppable~ ~boots~
BUT_ONLY
COPY_EXISTING ~baldur.bcs~ ~override~ //Better notifications, distinguish from de'Arnise
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE
~SetGlobalTimer("BardMoneyTime","GLOBAL",SEVEN_DAYS)[%G_BLANK%]+DisplayString(Player1,47142)~
~SetGlobalTimer("BardMoneyTime","GLOBAL",SEVEN_DAYS)
DisplayString(Player1,@136)~ //Theater earnings are ready.
  END
BUT_ONLY


//Fighter keep
COPY_EXISTING ~baldur.bcs~ ~override~ //create keep messenger next to the player
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY CASE_INSENSITIVE
      ~CreateCreatureObjectOffScreen("kprunn01",Player1,0,0,0)~
      ~CreateCreatureObject("kprunn01",Player1,0,0,0)~
  END
BUT_ONLY
COPY_EXISTING ~kprunn01.cre~ ~override~ //speed up keep messenger
  ADD_CRE_ITEM ~boot01~ #0 #0 #0 ~unstealable&undroppable~ ~boots~
BUT_ONLY
<<<<<<<< .../tnt-inlined/improved_strongholds/fighter.baf
IF
  GlobalTimerExpired("KPPlotTime","GLOBAL")
  !Global("KeepPlot","GLOBAL",14)
  !Global("RoenalControl","GLOBAL",1)
  !Global("Chapter","GLOBAL",4)
  !Global("Chapter","GLOBAL",5)
  !Global("Chapter","GLOBAL",7)
  Global("g_tnt_fighter_notif","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("g_tnt_fighter_notif","GLOBAL",1)
    DisplayString(Player1,@140) //The people of de'Arnise keep need presence of their Lord.
END
IF
  GlobalTimerNotExpired("KPPlotTime","GLOBAL")
  Global("g_tnt_fighter_notif","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("g_tnt_fighter_notif","GLOBAL",0)
END
>>>>>>>>
EXTEND_BOTTOM ~baldur.bcs~ ~.../tnt-inlined/improved_strongholds/fighter.baf~ EVAL


//Druid
<<<<<<<< .../tnt-inlined/improved_strongholds/druid.baf
IF
  Global("DruidStronghold","GLOBAL",1)
  Global("DruidQuest1","GLOBAL",0)
  Global("g_tnt_druid_notif","GLOBAL",0)
  !Global("Chapter","GLOBAL",4)
  !Global("Chapter","GLOBAL",5)
  !Global("Chapter","GLOBAL",7)
THEN
  RESPONSE #100
    SetGlobal("g_tnt_druid_notif","GLOBAL",1)
    DisplayString(Player1,@138) //The Grove awaits.
END

IF
  GlobalTimerNotExpired("DruidQuest2Begin","GLOBAL")
  Global("g_tnt_druid_notif","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("g_tnt_druid_notif","GLOBAL",0)
END

IF
  GlobalTimerExpired("DruidQuest2Begin","GLOBAL")
  Global("DruidStronghold","GLOBAL",1)
  Global("DruidQuest2","GLOBAL",0)
  Global("g_tnt_druid_notif","GLOBAL",0)
  !Global("Chapter","GLOBAL",4)
  !Global("Chapter","GLOBAL",5)
  !Global("Chapter","GLOBAL",7)
THEN
  RESPONSE #100
    SetGlobal("g_tnt_druid_notif","GLOBAL",1)
    DisplayString(Player1,@138) //The Grove awaits.
END
>>>>>>>>
EXTEND_BOTTOM ~baldur.bcs~ ~.../tnt-inlined/improved_strongholds/druid.baf~ EVAL



// Jan quest
COPY_EXISTING ~beeloo.cre~ ~override~ //speed up Beeloo
  ADD_CRE_ITEM ~boot01~ #0 #0 #0 ~unstealable&undroppable~ ~boots~
BUT_ONLY
