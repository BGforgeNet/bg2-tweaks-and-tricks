// https://forums.beamdog.com/discussion/61077/appending-to-ids-files-or-replacing-them
DEFINE_ACTION_FUNCTION g_add_ids_unique
  STR_VAR ids = ~~ label = ~~
  RET index
BEGIN
  OUTER_SET index = ~-1~
  ACTION_IF FILE_CONTAINS_EVALUATED (~%ids%.ids~ ~^.+[ %TAB%]%label%\b~) BEGIN
    COPY_EXISTING ~%ids%.ids~ override
    COUNT_2DA_ROWS 2 rows
    FOR (i = 0; i < rows; ++i) BEGIN
      READ_2DA_ENTRY i 1 2 state_label
      PATCH_IF ~%state_label%~ STRING_EQUAL_CASE ~%label%~ BEGIN
        READ_2DA_ENTRY i 0 2 state_id
      SET index = state_id
      END
    END
  BUT_ONLY
  END ELSE BEGIN
    ACTION_IF ((~%label%~ STRING_CONTAINS_REGEXP ~ ~) = 0) BEGIN
      FAIL ~IDS labels cannot have spaces~
    END ELSE BEGIN
      COPY_EXISTING ~%ids%.ids~ override
        COUNT_2DA_ROWS 2 rows
          FOR (i = 1; i < rows; ++i) BEGIN
            READ_2DA_ENTRY i 0 2 state_id
            SET $occupied_slot(~%state_id%~) = 1
          END
          FOR (i = 0; i < 256; ++i) BEGIN
            PATCH_IF (!VARIABLE_IS_SET $occupied_slot(~%i%~)) BEGIN
              index = i
              i = 256
              PATCH_IF index <= rows BEGIN
                INSERT_2DA_ROW index 2 ~%index% %label%~
              END ELSE BEGIN
                INSERT_2DA_ROW rows 2 ~%index% %label%~
              END
            END
          END
        PATCH_IF (index = ~-1~) BEGIN PATCH_FAIL ~No available slots in %ids%.ids~ END
      BUT_ONLY
    END
  END
END
