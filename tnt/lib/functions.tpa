APPEND ~ea.ids~ ~0 ANYONE~ UNLESS ~ANYONE~ // so that IDS_OF_SYMBOL works properly
APPEND ~state.ids~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~
CLEAR_IDS_MAP

// list web spells to build immunity
DEFINE_ACTION_FUNCTION list_web_spells
  RET_ARRAY spells
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~dw#0w215.spl~ BEGIN //SCS spider shapeshift token
    ACTION_DEFINE_ARRAY spells BEGIN spin683 spin575 spdr201 spwi215 dw#0w215 END
  END ELSE BEGIN
    ACTION_DEFINE_ARRAY spells BEGIN spin683 spin575 spdr201 spwi215 END
  END  
END

DEFINE_PATCH_FUNCTION unstack_armor_bonus
  INT_VAR bonus = 0 stacking_id_base = 0
BEGIN
  found = 0
  GET_OFFSET_ARRAY fx_offs ITM_V10_GEN_EFFECTS
  PHP_EACH fx_offs AS int => fx_off BEGIN
    READ_SHORT fx_off opcode
    PATCH_IF opcode == OPCODE_ac_vs_damage_type_mod BEGIN
      READ_LONG (fx_off + EFF_parameter2) type
      PATCH_IF type == AC_MOD_TYPE_set_base BEGIN
        READ_LONG (fx_off + EFF_parameter1) ac_mod
        WRITE_LONG (fx_off + EFF_parameter1) (ac_mod + bonus)
        found = 1
      END
    END
  END
  PATCH_IF found == 1 BEGIN
    FOR (i=1;i<=bonus;++i) BEGIN
      LPF ADD_ITEM_EQEFFECT
        INT_VAR
          opcode = OPCODE_ac_vs_damage_type_mod
          target = TARGET_EFF_self
          timing = TIMING_while_equipped
          insert_point = "-1"
          parameter1 = 1 // amount
          parameter2 = AC_MOD_TYPE_all
          savingthrow = FLAG_SAVINGTHROW_tobex_limit_effect_stacking
          special = (stacking_id_base + i)
          probability1 = (100 + i) // see main.tpa
      END
    END
  END
END

DEFINE_ACTION_FUNCTION CREATE_SHELL_SPELL
  INT_VAR duration = 0
  STR_VAR
    spell = ~~
    type = ~~
BEGIN
  LAF CREATE_SPELL
    INT_VAR flags = FLAG_SPL_ignore_dead_wild_magic
    STR_VAR spell
  END
  ACTION_MATCH 1 WITH
    ~blindness~ STR_EQ ~%type%~ BEGIN LAF CREATE_SHELL_BLINDNESS INT_VAR duration STR_VAR spell END END
    DEFAULT WARN ~CREATE_SHELL_SPELL: unknown type %type%~
  END
END

DEFINE_ACTION_FUNCTION CREATE_SHELL_BLINDNESS
  INT_VAR duration = 0
  STR_VAR spell = ~~
BEGIN
  COPY_EXISTING ~%spell%.spl~ ~override~
    target = TARGET_EFF_preset
    timing = TIMING_duration

    LPF ADD_SPELL_HEADER INT_VAR target = TARGET_HEADER_creature END
    LPF ADD_SPELL_EFFECT INT_VAR opcode = OPCODE_blindness target timing duration END
    LPF ADD_SPELL_EFFECT
      INT_VAR
        opcode = OPCODE_colour_glow_pulse
        target timing duration
        parameter1 = 0x7f7f7f00 // rgb
        parameter2 = 0x14 // cycle speed = 20
    END
    blinded_strref = 1474
    LPF ADD_SPELL_EFFECT
      INT_VAR
        target
        opcode = OPCODE_text_display_string
        timing = TIMING_instant
        parameter1 = blinded_strref
    END
    LPF ADD_SPELL_EFFECT
      INT_VAR
        target timing duration
        opcode = OPCODE_display_special_effect_icon
        parameter2 = ICON_blind
    END
    LPF ADD_SPELL_EFFECT
      INT_VAR
        target duration
        opcode = OPCODE_play_sound_effect
        timing = TIMING_delayed
      STR_VAR resource = ~eff_e07.wav~
    END
    PATCH_FOR_EACH vvc IN ~sph1hi01~ ~sphlhi02~ BEGIN
      LPF ADD_SPELL_EFFECT
        INT_VAR
          target duration
          opcode = OPCODE_play_3d_effect
          timing = TIMING_instant
          duration = 3
          parameter2 = 1 // play over target attached
        STR_VAR resource = ~%vvc%~
      END
    END
  BUT_ONLY
END
