DEFINE_PATCH_FUNCTION GET_2DA_INDEX
  RET_ARRAY row_index col_index
BEGIN
  COUNT_2DA_COLS columns
  PATCH_PRINT "columns: %columns%"
  READ_2DA_ENTRIES_NOW rows (columns - 1) //top row/first column is normally empty
  PATCH_PRINT "rows: %rows%"
  FOR (c=1; c<columns; ++c) BEGIN
    READ_2DA_ENTRY_FORMER rows 0 c name
    PATCH_PRINT "col: %c%, name: %name%"
    TEXT_SPRINT $col_index("%name%") c
  END
  FOR (r=1; r<rows; ++r) BEGIN
    READ_2DA_ENTRY_FORMER rows r 0 name
    TEXT_SPRINT $row_index("%name%") r
  END
END

DEFINE_PATCH_FUNCTION g_replace_strref
  INT_VAR
    offset = 0
    old_substring_traref = 0
    new_substring_traref = 0
BEGIN
  READ_STRREF offset old_string ELSE ~~
  READ_LONG offset old_string_num
  PATCH_IF (NOT (old_string STRING_EQUAL ~~))
            AND (old_string_num != "-1")
  BEGIN
    SPRINT old_substring (AT old_substring_traref)
    SPRINT new_substring (AT new_substring_traref)
//    PATCH_PRINT ~old string: %old_string%~
//    PATCH_PRINT ~old substring: %old_substring%~
//    PATCH_PRINT ~new substring: %new_substring%~
    INNER_PATCH_SAVE new_string ~%old_string%~ BEGIN
      REPLACE_TEXTUALLY ~%old_substring%~ ~%new_substring%~
    END
//    PATCH_PRINT ~new string: %new_string%~
    SAY_EVALUATED offset ~%new_string%~
  END
END

// list web spells to build immunity
DEFINE_ACTION_FUNCTION list_web_spells
  RET_ARRAY spells
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~dw#0w215.spl~ BEGIN //SCS spider shapeshift token
    ACTION_DEFINE_ARRAY spells BEGIN spin683 spin575 spdr201 spwi215 dw#0w215 END
  END ELSE BEGIN
    ACTION_DEFINE_ARRAY spells BEGIN spin683 spin575 spdr201 spwi215 END
  END  
END

// ADD_ITEM_HEADER from https://github.com/Gibberlings3/ItemRevisions/blob/master/item_rev/lib/macros.tpa
INCLUDE ~%MOD_FOLDER%/lib/external/ir.tpa~
