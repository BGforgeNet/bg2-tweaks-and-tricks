DEFINE_PATCH_FUNCTION GET_2DA_INDEX
  RET_ARRAY col_index row_index
BEGIN
  COUNT_2DA_COLS columns
  READ_2DA_ENTRIES_NOW rows (columns - 1)//top row/first column is normally empty
  FOR (c=0; c<(columns - 1); ++c) BEGIN //weidu counts from the first field, so first named column starts from index 0, too ("shifted to the left" by one)
    READ_2DA_ENTRY_FORMER rows 0 c name
    c2 = c + 1 // shift back to right
    DEFINE_ASSOCIATIVE_ARRAY col_index BEGIN "%name%" => ~%c2%~ END
  END
  FOR (r=1; r<rows; ++r) BEGIN
    READ_2DA_ENTRY_FORMER rows r 0 name
    DEFINE_ASSOCIATIVE_ARRAY row_index BEGIN "%name%" => ~%r%~ END
  END
END

DEFINE_PATCH_FUNCTION g_replace_strref
  INT_VAR
    offset = 0
    old_substring_traref = 0
    new_substring_traref = 0
BEGIN
  READ_STRREF offset old_string ELSE ~~
  READ_LONG offset old_string_num
  PATCH_IF (NOT (old_string STRING_EQUAL ~~))
            AND (old_string_num != "-1")
  BEGIN
    SPRINT old_substring (AT old_substring_traref)
    SPRINT new_substring (AT new_substring_traref)
    INNER_PATCH_SAVE new_string ~%old_string%~ BEGIN
      REPLACE_TEXTUALLY ~%old_substring%~ ~%new_substring%~
    END
    SAY_EVALUATED offset ~%new_string%~
  END
END

// list web spells to build immunity
DEFINE_ACTION_FUNCTION list_web_spells
  RET_ARRAY spells
BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~dw#0w215.spl~ BEGIN //SCS spider shapeshift token
    ACTION_DEFINE_ARRAY spells BEGIN spin683 spin575 spdr201 spwi215 dw#0w215 END
  END ELSE BEGIN
    ACTION_DEFINE_ARRAY spells BEGIN spin683 spin575 spdr201 spwi215 END
  END  
END

// ADD_ITEM_HEADER from https://github.com/Gibberlings3/ItemRevisions/blob/master/item_rev/lib/macros.tpa
INCLUDE ~%MOD_FOLDER%/lib/external/ir.tpa~
INCLUDE ~%MOD_FOLDER%/lib/external/ids.tpa~

DEFINE_ACTION_FUNCTION CREATE_MINION
  STR_VAR
    cre = ~~
    script = ~~
    name = ~-1~
    ea = ~~
BEGIN
  COPY_EXISTING ~cutspy.cre~ ~override/%cre%.cre~
    PATCH_IF NOT ~%name%~ STR_EQ ~-1~ BEGIN
      SAY_EVALUATED NAME1 ~%name%~
    END
    WRITE_ASCIIT DEATHVAR ~%cre%~
    WRITE_ASCIIT SCRIPT_OVERRIDE ~%script%~
    PATCH_IF NOT ~%ea%~ STR_EQ ~~ BEGIN
      WRITE_BYTE CRE_allegiance IDS_OF_SYMBOL (~ea~ ~%ea%~)
    END
    WRITE_LONG CRE_animation IDS_OF_SYMBOL (~animate~ ~DOOM_GUARD~) // https://forums.beamdog.com/discussion/73632/a-few-uses-for-invisible-minions
    LPF DELETE_EFFECT INT_VAR match_opcode = OPCODE_clear_fog_of_war_wizard_eye END
    LPF DELETE_EFFECT INT_VAR match_opcode = OPCODE_avatar_removal END // this seems to block shouts for some reason, or maybe even preventing creature scripts from running altogether
    LPF ADD_CRE_EFFECT INT_VAR opcode = OPCODE_animation_removal timing = TIMING_permanent parameter2 = 1 END
    LPF ADD_CRE_EFFECT INT_VAR opcode = OPCODE_selection_circle_removal timing = TIMING_permanent END
  BUT_ONLY
END

DEFINE_PATCH_FUNCTION ALTER_AREA_REGION2
  INT_VAR
    index = "-1"
    trapped = "-1"
    trap_detect = "-1"
    trap_remove = "-1"
    flag_trap_detectable = "-1"
  STR_VAR
    match_script = "-1"
BEGIN
  GET_OFFSET_ARRAY regions ARE_V10_REGIONS
  PHP_EACH regions AS int => r_off BEGIN
    READ_ASCII (r_off + ARE_REGION_script) script
    PATCH_IF ~%script%~ STR_EQ ~%match_script%~ BEGIN
      PATCH_IF trapped != "-1" BEGIN WRITE_LONG (r_off + ARE_REGION_trapped) 1 END
      PATCH_IF trap_detect != "-1" BEGIN WRITE_LONG (r_off + ARE_REGION_trap_detect) trap_detect END
      PATCH_IF trap_remove != "-1" BEGIN WRITE_LONG (r_off + ARE_REGION_trap_remove) trap_remove END
      PATCH_IF flag_trap_detectable == 0 BEGIN
        WRITE_LONG (r_off + ARE_REGION_flags) THIS BAND BNOT FLAG_ARE_REGION_detectable
      END
      PATCH_IF flag_trap_detectable == 1 BEGIN
        WRITE_LONG (r_off + ARE_REGION_flags) THIS BOR FLAG_ARE_REGION_detectable
      END
    END
  END
END
