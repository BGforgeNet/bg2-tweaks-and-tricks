INCLUDE ~%comp_dir%/sound_list.tpa~ // define a sound_list array with compliments and insults

DEFINE_ACTION_FUNCTION get_framerate
  RET framerate
BEGIN
  OUTER_SET framerate = 30
  ACTION_IF GAME_IS ~bgt~ BEGIN
    COPY ~baldur.ini~ ~baldur.ini~
      SPRINT string ~~
      offset = 0
      WHILE !(~Rate=~ STRING_EQUAL ~%string%~) BEGIN
        READ_ASCII offset string (5)
        offset = (offset + 1)
      END
      offset = (offset + 4)
      READ_ASCII offset framerate (2)
      PATCH_PRINT ~Current framerate is: %framerate%~
    BUT_ONLY
  END
  ACTION_IF GAME_IS ~eet~ BEGIN
    ACTION_IF FILE_EXISTS ~%USER_DIRECTORY%/baldur.lua~ BEGIN
      ACTION_IF FILE_CONTAINS ~%USER_DIRECTORY%/baldur.lua~ ~Rate','~ BEGIN
        COPY ~%USER_DIRECTORY%/baldur.lua~ ~%USER_DIRECTORY%/baldur.lua~
          SPRINT string ~~
          offset = 0
          WHILE !(~Rate','~ STRING_EQUAL ~%string%~) BEGIN
            READ_ASCII offset string (7)
            offset = (offset + 1)
          END
          offset = (offset + 6)
          READ_ASCII offset framerate (2)
          PATCH_PRINT ~Current framerate is: %framerate%~
        BUT_ONLY
      END ELSE BEGIN PRINT ~framerate not found in %USER_DIRECTORY%/baldur.lua, using default = 30~ END
    END ELSE BEGIN PRINT ~%USER_DIRECTORY%/baldur.lua not found, using framerate default = 30~ END
  END
END

DEFINE_ACTION_FUNCTION decompress_sound
  STR_VAR filename = ~~
BEGIN
  OUTER_SPRINT tools_dir ~%MOD_FOLDER%/tools~
  OUTER_SET rc = 1
  ACTION_IF GAME_IS ~bgt~ BEGIN
    ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
      AT_NOW rc ~%tools_dir%\wavc.exe override\%sound%.wav override\%sound%.wav~
    END ELSE BEGIN
      AT_NOW rc ~wine %tools_dir%/wavc.exe override/%sound%.wav override/%sound%.wav~ EXACT
    END
  END
  ACTION_IF GAME_IS ~eet~ BEGIN
    COPY + ~override/%sound%.wav~ ~override/g_temp.wavc~ // oggdec needs an intermediate file
    ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
      AT_NOW rc ~%tools_dir%\oggdec.exe override\g_temp.wavc -o override\%sound%.wav~
    END
    ACTION_IF ~%WEIDU_OS%~ STR_EQ ~unix~ BEGIN
      AT_NOW rc ~oggdec override/g_temp.wavc -o override/%sound%.wav~
    END
    ACTION_IF ~%WEIDU_OS%~ STR_EQ ~mac~ BEGIN
      AT_NOW rc ~sox override/g_temp.wavc override/%sound%.wav~
    END
    DELETE + ~override/g_temp.wavc~
  END
  ACTION_IF rc != 0 BEGIN
    FAIL ~Failed to decompress %sound% wavc~
  END
END

LAF get_framerate RET framerate END

ACTION_IF framerate != 30 BEGIN
  // Copy wavc sounds and decompress to wav

  ACTION_PHP_EACH sound_list AS index => sound BEGIN
    COPY_EXISTING ~%sound%.wav~ ~override/%sound%.wav~
    LAF decompress_sound STR_VAR filename = ~%sound%.wav~ END
  END

  OUTER_SET wav_filesize_off = 0x4
  OUTER_SET wav_datesize_off = 0x2a
  //adjust WAV
  ACTION_PHP_EACH sound_list AS index => sound BEGIN
    COPY_EXISTING ~%sound%.wav~ ~override~
      filesize = (SOURCE_SIZE - 8) //without header
      datasize = (SOURCE_SIZE - 44) //actual data size
      //lengthen it
      delta = (datasize*framerate/30 - datasize) //BG1 original frame rate is 30
      WRITE_LONG wav_filesize_off (filesize + delta)
      WRITE_LONG wav_datesize_off (datasize + delta)
      INSERT_BYTES SOURCE_SIZE delta
    BUT_ONLY
  END

  PRINT "Sounds are adjusted to %framerate% FPS"
END
