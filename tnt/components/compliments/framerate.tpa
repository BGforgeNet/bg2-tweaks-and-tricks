INCLUDE ~%comp_dir%/sound_list.tpa~ // define a sound_list array with compliments and insults

DEFINE_ACTION_FUNCTION get_framerate
  RET framerate
BEGIN
  OUTER_SET framerate = 30
  ACTION_IF GAME_IS ~bgt~ BEGIN
    COPY ~baldur.ini~ ~baldur.ini~
      SPRINT string ~~
      offset = 0
      WHILE !(~Rate=~ STRING_EQUAL ~%string%~) BEGIN
        READ_ASCII offset string (5)
        offset = (offset + 1)
      END
      offset = (offset + 4)
      READ_ASCII offset framerate (2)
      PATCH_PRINT ~Current framerate is: %framerate%~
    BUT_ONLY
  END
  ACTION_IF GAME_IS ~eet~ BEGIN
    ACTION_IF FILE_EXISTS ~%USER_DIRECTORY%/baldur.lua~ BEGIN
      ACTION_IF FILE_CONTAINS ~%USER_DIRECTORY%/baldur.lua~ ~Rate','~ BEGIN
        COPY ~%USER_DIRECTORY%/baldur.lua~ ~%USER_DIRECTORY%/baldur.lua~
          SPRINT string ~~
          offset = 0
          WHILE !(~Rate','~ STRING_EQUAL ~%string%~) BEGIN
            READ_ASCII offset string (7)
            offset = (offset + 1)
          END
          offset = (offset + 6)
          READ_ASCII offset framerate (2)
          PATCH_PRINT ~Current framerate is: %framerate%~
        BUT_ONLY
      END ELSE BEGIN PRINT ~framerate not found in baldur.lua, using default = 30~ END
    END ELSE BEGIN PRINT ~baldur.lua not found, using framerate default = 30~ END
  END
END

DEFINE_ACTION_FUNCTION decompress_sound
  STR_VAR filename = ~~
BEGIN
  OUTER_SPRINT tools_dir ~%MOD_FOLDER%/tools~
  OUTER_SET rc = 1
  ACTION_IF GAME_IS ~bgt~ BEGIN
    ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
      AT_NOW rc ~%tools_dir%\wavc.exe override\%filename% override\%filename%~
    END ELSE BEGIN
      AT_NOW rc ~WINEDEBUG=-all wine %tools_dir%/wavc.exe override/%filename% override/%filename%~ EXACT
    END
  END
  ACTION_IF GAME_IS ~eet~ BEGIN
    OUTER_SPRINT tempfile ~override/g_temp.wavc~
    COPY + ~override/%filename%~ ~%tempfile%~ // oggdec needs an intermediate file
    ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
      AT_NOW rc ~%tools_dir%\oggdec.exe %tempfile% -w override\%filename%~
    END
    ACTION_IF ~%WEIDU_OS%~ STR_EQ ~unix~ BEGIN
      AT_NOW rc ~oggdec %tempfile% -o override/%filename%~
    END
    ACTION_IF ~%WEIDU_OS%~ STR_EQ ~mac~ BEGIN
      AT_NOW rc ~sox %tempfile% override/%filename%~
    END
    DELETE + ~%tempfile%~
  END
  ACTION_IF rc != 0 BEGIN
    FAIL ~Failed to decompress %filename%~
  END
END

LAF get_framerate RET framerate END

ACTION_IF framerate != 30 BEGIN
  // Copy wavc sounds and decompress to wav

  ACTION_PHP_EACH sound_list AS sound => dummy BEGIN
    COPY_EXISTING ~%sound%.wav~ ~override/%sound%.wav~
    LAF decompress_sound STR_VAR filename = ~%sound%.wav~ END
  END

  OUTER_SET wav_filesize_off = 0x4
  // http://soundfile.sapp.org/doc/WaveFormat/
  OUTER_SET wav_datasize_off = 0x28
  // adjust WAV
  ACTION_PHP_EACH sound_list AS sound => dummy BEGIN
    COPY_EXISTING ~%sound%.wav~ ~override~
      filesize = (SOURCE_SIZE - 8)  // without header
      datasize = (SOURCE_SIZE - 44) // actual data size
      //lengthen it
      delta = (datasize*framerate/30 - datasize)  // BG1 original frame rate is 30
      WRITE_LONG wav_filesize_off (filesize + delta)
      WRITE_LONG wav_datasize_off (datasize + delta)
      INSERT_BYTES SOURCE_SIZE delta
    BUT_ONLY
  END

  PRINT "Sounds are adjusted to %framerate% FPS"
END
