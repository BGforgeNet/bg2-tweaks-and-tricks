INCLUDE ~%comp_dir%/sound_list.tpa~ // define a sound_list array with compliments and insults

DEFINE_ACTION_FUNCTION get_framerate
  RET framerate
BEGIN
  OUTER_SET framerate = 30
  ACTION_IF GAME_IS ~bgt~ BEGIN
    COPY ~baldur.ini~ ~baldur.ini~
      SPRINT string ~~
      offset = 0
      WHILE !(~Rate=~ STRING_EQUAL ~%string%~) BEGIN
        READ_ASCII offset string (5)
        SET offset = (offset + 1)
      END
      offset = (offset + 4)
      READ_ASCII offset framerate (2)
      PATCH_PRINT ~Current framerate is: %framerate%~
    BUT_ONLY
  END
  ACTION_IF GAME_IS ~eet~ BEGIN
    ACTION_TRY
      COPY ~%USER_DIRECTORY%/baldur.lua~ ~%USER_DIRECTORY%/baldur.lua~
        SPRINT string ~~
        offset = 0
        WHILE !(~Rate','~ STRING_EQUAL ~%string%~) BEGIN
          READ_ASCII offset string (7)
          SET offset = (offset + 1)
        END
        offset = (offset + 6)
        READ_ASCII offset framerate (2)
        PATCH_PRINT ~Current framerate is: %framerate%~
      BUT_ONLY
      WITH ~g_dummy~ BEGIN END
      DEFAULT PRINT ~framerate not found in %USER_DIRECTORY%/baldur.lua, using default=30~
    END
  END
END

LAF get_framerate RET framerate END

// Copy wavc sounds and decompress to wav
OUTER_SPRINT tools_dir ~%MOD_FOLDER%/tools~
ACTION_PHP_EACH sound_list AS index => sound BEGIN
  COPY_EXISTING ~%sound%.wav~ ~override~
  ACTION_IF ~%WEIDU_OS%~ STR_EQ ~win32~ BEGIN
    AT_NOW rc ~%tools_dir%\wavc.exe override\%sound%.wav override\%sound%.wav~
  END ELSE BEGIN
    AT_NOW rc ~wine %tools_dir%/wavc.exe override/%sound%.wav override/%sound%.wav~ EXACT
  END
  ACTION_IF rc != 0 BEGIN
    FAIL ~Failed to decompress %sound% wavc~
  END
END

OUTER_SET wav_filesize_off = 0x4
OUTER_SET wav_datesize_off = 0x2a
//adjust WAV
ACTION_PHP_EACH sound_list AS index => sound BEGIN
  COPY_EXISTING ~%sound%.wav~ ~override~
    filesize = (SOURCE_SIZE - 8) //without header
    datasize = (SOURCE_SIZE - 44) //actual data size
    //lengthen it
    delta = (datasize*framerate/30 - datasize) //BG1 original frame rate is 30
    WRITE_LONG wav_filesize_off (filesize + delta)
    WRITE_LONG wav_datesize_off (datasize + delta)
    INSERT_BYTES SOURCE_SIZE delta
  BUT_ONLY
END

PRINT "Sounds are adjusted to %framerate% FPS"
