/* paladin: permanent protection aura https://adnd2e.fandom.com/wiki/Paladin#Protection_from_Evil */

INCLUDE ~%lib_dir%/tobex.tpa~
OUTER_SET stacking_id = 4

// THAC0 penalty effect
CREATE EFF VERSION "V2.0" ~g_palpfe~
    WRITE_LONG EFF2_opcode OPCODE_to_hit_mod
    WRITE_LONG EFF2_target TARGET_FX_preset
    WRITE_SHORT EFF2_timing TIMING_duration
    WRITE_LONG EFF2_parameter1 1  // penalty
    WRITE_LONG EFF2_parameter2 MOD_TYPE_cumulative
    WRITE_LONG EFF2_duration 3  // small duration
    // non-EE: prevent stacking
    WRITE_LONG EFF2_stacking_id_tobex (effect_stacking_id_base + stacking_id)

// Repeating effect, casts "aura" spell
CREATE EFF VERSION "V2.0" ~g_palpf2~
    WRITE_LONG EFF2_opcode OPCODE_cast_spell_at_creature
    WRITE_LONG EFF2_target TARGET_FX_self
    WRITE_SHORT EFF2_timing TIMING_instant
    WRITE_ASCIIT EFF2_resource ~g_palpfe~

// projectile
ADD_PROJECTILE ~%comp_dir%/g_palpfe.pro~  // inareasm
OUTER_SET pro = g_palpfe

// aura spell: THAC0 penalty
CREATE SPL ~g_palpfe~
    // EE: remove self if present
    PATCH_IF ENGINE_IS ~bgee bg2ee pstee iwdee eet~ BEGIN
        LPF ADD_SPELL_EFFECT
            INT_VAR
                opcode = OPCODE_remove_effects_by_resource
                target = TARGET_FX_preset
                parameter2 = 2  // only timed effects
                timing = TIMING_instant
                dispel_bypass = BYPASS_MR | BYPASS_DEFLECTION
            STR_VAR
                resource = ~g_palpfe~
        END
    END

    LPF ADD_SPELL_EFFECT
        INT_VAR
            opcode = OPCODE_use_eff_file
            target = TARGET_FX_preset
            parameter1 = IDS_align
            parameter2 = 3  // MASK_EVIL
            timing = TIMING_instant
            dispel_bypass = BYPASS_MR | BYPASS_DEFLECTION
        STR_VAR
            resource = ~g_palpfe~
    END
    WRITE_SHORT SPL_HEAD_projectile pro

// CLAB spell
CREATE SPL ~g_palpf2~
    LPF ADD_SPELL_EFFECT
        INT_VAR
            opcode = OPCODE_apply_repeating_eff
            target = TARGET_FX_self
            parameter1 = 1  // amount 1 - just need non-zero
            // parameter2 = 0: apply each second
        STR_VAR
            resource = ~g_palpf2~
    END

// Add to kit tables from level 1
ACTION_FOR_EACH i IN 1 2 3 4 BEGIN
    APPEND ~clabrn0%i%.2da~
        ~g_pala_aura    AP_g_palpf2        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****   ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****        ****~
    COPY_EXISTING ~clabpa0%i%.2da~ ~override~
        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~GA_SPCL213~ ~*~
        PRETTY_PRINT_2DA
    BUT_ONLY
END
