// Branwen
<<<<<<<< .../tnt-inlined/romance_cheats/branwen_match.baf
IF
    Global("P#BranMatch","GLOBAL",0)
    InParty(Myself)
THEN
    RESPONSE #100
        SetGlobal("P#BranMatch","GLOBAL",1)
END
>>>>>>>>
ACTION_IF FILE_EXISTS "override/x#branwenromance.g3" BEGIN
    PRINT ~BG1NPC: Branwen~
    COPY_EXISTING ~%BRANWEN_BCS%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            // Dynaheir checks
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]Global("X#DynaheirRomanceActive","GLOBAL",[12])~ ~ False()~
            // Shar-Teel checks
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]Global("X#SharInterest","GLOBAL",[12])~ ~ False()~
            // shouldn't need this, but just in case
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]ReputationLT(Player1,10)~ ~~
        END 
    BUT_ONLY
    // custom match all
    EXTEND_TOP ~%BRANWEN_BCS%.bcs~ ~.../tnt-inlined/romance_cheats/branwen_match.baf~
END


// Dynaheir
<<<<<<<< .../tnt-inlined/romance_cheats/dynaheir_match.baf
IF
    Global("X#DynahMatch","GLOBAL",0)
    InParty(Myself)
THEN
    RESPONSE #100
        SetGlobal("X#DynahMatch","GLOBAL",1)
END
>>>>>>>>
ACTION_IF FILE_EXISTS "override/x#dynaheirromance.g3" BEGIN
    PRINT ~BG1NPC: Dynaheir~

    COPY_EXISTING ~%DYNAHEIR_BCS%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            // Branwen checks
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]Global("P#BranwenRomanceActive","GLOBAL",[12])~ ~ False()~
            // kill rep check            
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~ReputationGT(Player1,[0-9]+)~ ~~
        END 
    BUT_ONLY
    // custom match all
    EXTEND_TOP ~%DYNAHEIR_BCS%.bcs~ ~.../tnt-inlined/romance_cheats/dynaheir_match.baf~

    COPY_EXISTING ~%DYNAHEIR_DREAM%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            // kill rep check
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]ReputationGT(Player1,11)~ ~~
        END
    BUT_ONLY
END

// Shar-Teel
<<<<<<<< .../tnt-inlined/romance_cheats/sharteel_match.baf
IF
    Global("X#SharMatch","GLOBAL",0)
    InParty(Myself)
THEN
    RESPONSE #100
        SetGlobal("X#SharMatch","GLOBAL",1)
END
>>>>>>>>
ACTION_IF FILE_EXISTS "override/x#sharteelromance.g3" BEGIN
    PRINT ~BG1NPC: Shar-Teel~

    COPY_EXISTING ~%SHARTEEL_BCS%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            // Branwen checks
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]Global("P#BranwenRomanceActive","GLOBAL",[12])~ ~ False()~
            // kill romance repuration checks
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]ReputationLT(Player1,11)~ ~~
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]ReputationGT(Player1,12)~ ~False()~
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]ReputationGT(Player1,15)~ ~False()~
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]ReputationGT(Player1,15)~ ~False()~
            // don't let her leave the party because of the reputation
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]Global("X#ShHighReputation","GLOBAL",[01])~ ~ False()~
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]Global("X#ShHighMale","GLOBAL",[01])~ ~ False()~
        END
    BUT_ONLY
    // custom match all
    EXTEND_TOP ~%SHARTEEL_BCS%.bcs~ ~.../tnt-inlined/romance_cheats/sharteel_match.baf~

    COPY_EXISTING ~%SHARTEEL_DREAM%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            // kill romance repuration checks
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]ReputationLT(Player1,11)~ ~~
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]ReputationLT(Player1,13)~ ~~
        END
    BUT_ONLY

    COPY_EXISTING ~%SHARTEEL_POST%.dlg~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            // make sure she doesn't leave for good after being kicked from party
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~^HappinessLT(Myself,0)~ ~False()~
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]*HappinessLT(Myself,0)~ ~False()~
        END
    BUT_ONLY

    COPY_EXISTING ~%SHARTEEL_JOINED%.dlg~ ~override~
        DECOMPILE_AND_PATCH BEGIN
            // not sure if necessary
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~^ReputationGT(Player1,13)~ ~False()~
            REPLACE_TEXTUALLY CASE_INSENSITIVE EVALUATE_REGEXP ~[ %TAB%]ReputationGT(Player1,13)~ ~False()~
        END
    BUT_ONLY
END
