OUTER_SET fail = 1
ACTION_IF FILE_EXISTS_IN_GAME ~spwi413c.spl~ BEGIN // bg2fixpack
  ACTION_DEFINE_ARRAY spells BEGIN ~spwi413a~ ~spwi413c~ ~spcl415a~ END
  OUTER_SET fail = 0
END ELSE BEGIN
  ACTION_IF ENGINE_IS ~soa tob~ BEGIN // vanilla, no fixpack
    ACTION_DEFINE_ARRAY spells BEGIN ~spwi413~ ~spcl415~ END
    OUTER_SET fail = 0
  END
  ACTION_IF ENGINE_IS ~bgee bg2ee~ BEGIN // EEs
    ACTION_DEFINE_ARRAY spells BEGIN ~spwi413a~ ~spcl415a~ END
    OUTER_SET fail = 0
  END
END

ACTION_IF fail == 1 BEGIN
  FAIL @32
END

ACTION_PHP_EACH spells AS i => spell BEGIN
  COPY_EXISTING ~%spell%.spl~ ~override~
    LPF DELETE_SPELL_EFFECT
      INT_VAR
        opcode_to_delete = OPCODE_hold_ii
    END
    LPF ADD_SPELL_EFFECT
      INT_VAR
        insert_point = 0
        opcode = OPCODE_visual_range // to disable using wands and stuff
        parameter1 = 0 // blind
        parameter2 = 1 // flat mod
        duration = 60
        target = TARGET_FX_preset
        resist_dispel = DISPEL | BYPASS_MR
    END
  BUT_ONLY
END
