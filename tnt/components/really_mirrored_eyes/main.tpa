OUTER_SET i = 0
OUTER_SET gaze_id = IDS_OF_SYMBOL(projectl ~GAZE~) + 1

COPY_EXISTING_REGEXP ~^.*\.spl$~ ~override~
  READ_STRREF NAME1 name
  // check for gaze projectile
  gaze = 0
  GET_OFFSET_ARRAY h_array SPL_V10_HEADERS
  PHP_EACH h_array AS int => h_off BEGIN
    READ_SHORT (h_off + SPL_HEADER_projectile) pro
    PATCH_IF pro == gaze_id BEGIN
      gaze = 1
    END
  END
  PATCH_IF gaze == 1 BEGIN
    PATCH_PRINT ~found gaze pro: %SOURCE_RES% - %name%~
    DEFINE_ASSOCIATIVE_ARRAY spell_list BEGIN ~%i%~ => ~%SOURCE_RES%~ END
    DEFINE_ASSOCIATIVE_ARRAY spell_names BEGIN ~%i%~ => ~%name%~ END
    i+=1
  END

  // check "gaze" in the name
  PATCH_IF gaze == 0 BEGIN
    READ_STRREF NAME1 name
    PATCH_IF (~%name%~ STRING_CONTAINS_REGEXP ~gaze~) == 0 BEGIN 
      DEFINE_ASSOCIATIVE_ARRAY spell_list BEGIN ~%i%~ => ~%SOURCE_RES%~ END //record resource
      DEFINE_ASSOCIATIVE_ARRAY spell_names BEGIN ~%i%~ => ~%name%~ END //record name
      PATCH_PRINT ~found gaze name: %SOURCE_RES% - %name%~
      i+=1
    END
  END
BUT_ONLY

COPY_EXISTING ~potn38.itm~ ~override~ //mirrored eyes
  j = 0
  GET_OFFSET_ARRAY h_array ITM_V10_HEADERS
  PHP_EACH h_array AS int => h_off BEGIN //find out which spells are already protected from
    GET_OFFSET_ARRAY2 fx_array h_off ITM_V10_HEAD_EFFECTS
    PHP_EACH fx_array AS int => fx_off BEGIN
      READ_SHORT fx_off fx_type
      PATCH_IF fx_type = OPCODE_protection_from_spell BEGIN
        READ_ASCII fx_off + FEATURE_resource spell
        DEFINE_ASSOCIATIVE_ARRAY old_list BEGIN ~%j%~ => ~%spell%~ END
        j+=1
      END
    END
  END
  PHP_EACH spell_list AS i => res BEGIN
    add = 1
    PHP_EACH old_list AS j => old_res BEGIN //skip spells that are already protected from
      PATCH_IF ~%old_res%~ STR_EQ ~%res%~ BEGIN
        add = 0
      END
    END
    PATCH_IF add == 1 BEGIN
      SPRINT name $spell_names(~%i%~)
      LPF ADD_ITEM_EFFECT
        INT_VAR
          opcode = OPCODE_protection_from_spell
          parameter1 = 0 //no feedback line
          duration = 60
          target = TARGET_FEATURE_self
          resist_dispel = DISPEL
          power = 4 //potion power
        STR_VAR
          resource = ~%res%~
      END
    END
  END
BUT_ONLY
