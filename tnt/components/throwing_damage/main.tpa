ACTION_DEFINE_ASSOCIATIVE_ARRAY typedamage BEGIN
  EVAL ~%ITM_TYPE_hammer%~ => 2 //hammer - crushing
  EVAL ~%ITM_TYPE_axe%~ => 3 //axe - slashing
END
ACTION_DEFINE_ASSOCIATIVE_ARRAY typetra BEGIN
  EVAL ~%ITM_TYPE_hammer%~ => 254 //crushing
  EVAL ~%ITM_TYPE_axe%~ => 255 //slashing
END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT ITM_type type
  PATCH_IF type == ITM_TYPE_axe OR type == ITM_TYPE_hammer BEGIN
    damage_type = $typedamage(~%type%~)
    damage_tra = $typetra(~%type%~)
    GET_OFFSET_ARRAY h_array ITM_V10_HEADERS
    PHP_EACH h_array AS int => h_off BEGIN
      READ_BYTE h_off attack_type
      PATCH_IF attack_type == ITM_ATTACK_TYPE_ranged BEGIN
        PATCH_IF damage == 1 BEGIN
          WRITE_SHORT (h_off + ITM_HEAD_damage_type) damage_type
          PATCH_FOR_EACH off IN IDENTIFIED_DESC UNIDENTIFIED_DESC BEGIN
            LPF REPLACE_STRREF_TRA
              INT_VAR
                offset = EVAL ~%off%~
                old_substring_traref = 253 //missile (piercing)
                new_substring_traref = damage_tra
            END
          END
        END
        PATCH_IF strength == 1 BEGIN
          WRITE_LONG (h_off + ITM_HEAD_flags) (THIS BAND FLAG_HEAD_add_strength_bonus)
        END
      END
    END
  END
BUT_ONLY
