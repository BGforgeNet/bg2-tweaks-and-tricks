ACTION_DEFINE_ASSOCIATIVE_ARRAY typedamage BEGIN
  ITM_TYPE_hammer => 2 //hammer - crushing
  ITM_TYPE_axe => 3 //axe - slashing
END
ACTION_DEFINE_ASSOCIATIVE_ARRAY typetra BEGIN
  ITM_TYPE_hammer => 254 //crushing
  ITM_TYPE_axe => 255 //slashing
END

INCLUDE ~%lib_dir%/macro.tpa~

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT ITM_type type
  PATCH_IF type == ITM_TYPE_axe OR type == ITM_TYPE_hammer BEGIN
    damage_type = $typedamage(~%type%~)
    damage_tra = $typetra(~%type%~)
    GET_OFFSET_ARRAY h_array ITM_V10_HEADERS
    PHP_EACH h_array AS int => h_off BEGIN
      READ_BYTE h_off attack_type
      PATCH_IF attack_type == ITM_ATTACK_TYPE_ranged BEGIN
        PATCH_IF damage == 1 BEGIN
          WRITE_SHORT (h_off + ITM_HEADER_damage_type) damage_type
          PATCH_FOR_EACH off IN DESC UNIDENTIFIED_DESC BEGIN
            LPF g_replace_strref
              INT_VAR
                offset = off
                old_substring_traref = 253 //missile (piercing)
                new_substring_traref = damage_tra
             END
          END
        END
        PATCH_IF strength == 1 BEGIN
          WRITE_LONG (h_off + ITM_HEADER_flags) (THIS BAND FLAG_HEADER_add_strength_bonus)
        END
      END
    END
  END
BUT_ONLY
