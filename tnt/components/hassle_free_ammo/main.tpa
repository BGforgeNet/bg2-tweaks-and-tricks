OUTER_SPRINT comp_dir ~%MOD_FOLDER%/hassle_free_ammo~

ACTION_DEFINE_ASSOCIATIVE_ARRAY item_name BEGIN
  axe => 160
  dagg => 162
  dart => 164
  arrow => 215
  bolt => 217
  bull => 219
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY item_desc BEGIN
  axe => 161
  dagg => 163
  dart => 165
  arrow => 216
  bolt => 218
  bull => 220
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY item_damagebonus BEGIN
  axe => 1
  dagg => 0
  dart => 0
  arrow => 0
  bolt => 0
  bull => 1
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY item_speed BEGIN
  axe => 4
  dagg => 2
  dart => 2
  arrow => 0
  bolt => 0
  bull => 0
END

ACTION_PHP_EACH item_name AS item => name BEGIN
  OUTER_SET desc = EVAL $item_desc(~%item%~)
  OUTER_SET damagebonus = EVAL $item_damagebonus(~%item%~)
  OUTER_SET speed = EVAL $item_speed(~%item%~)
  COPY ~%comp_dir%/g_%item%.itm~ ~override~
    SAY NAME1 ( AT name )
    SAY NAME2 ( AT name )
    WRITE_SHORT 0x42 0 //lore
    SAY UNIDENTIFIED_DESC ( AT desc )
    WRITE_LONG 0x54 "-1" //identified desc
    WRITE_LONG 0x34 300 //price
    WRITE_LONG 0x60 0 //enchantment

    //minor patching to ensure consistency
    GET_OFFSET_ARRAY ab_array ITM_V10_HEADERS
    PHP_EACH ab_array AS int => ab_off BEGIN
      //thac0 bonus
      WRITE_SHORT (ab_off + 0x14) 0

      //add strength bonus to axe, remove from others
      PATCH_IF (~%item%~ STRING_EQUAL ~axe~) BEGIN
        WRITE_LONG (ab_off + 0x26) (THIS BAND BIT0)
      END ELSE BEGIN
        WRITE_LONG (ab_off + 0x26) (THIS BAND BNOT BIT0)
      END

      //damage bonus
      WRITE_SHORT (ab_off + 0x1a) damagebonus

      //speed
      WRITE_BYTE (ab_off + 0x12) speed
    END

  BUT_ONLY
END


//add to Taerom and Ribald
ACTION_FOR_EACH store IN taerum ribald BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%store%.sto~ BEGIN
    COPY_EXISTING ~%store%.sto~ ~override~
      ADD_STORE_ITEM + "g_axe" #0 #0 #0 ~IDENTIFIED~ #10
      ADD_STORE_ITEM + "g_dagg" #0 #0 #0 ~IDENTIFIED~ #10
      ADD_STORE_ITEM + "g_dart" #0 #0 #0 ~IDENTIFIED~ #10
      ADD_STORE_ITEM + "g_arrow" #0 #0 #0 ~IDENTIFIED~ #10
      ADD_STORE_ITEM + "g_bolt" #0 #0 #0 ~IDENTIFIED~ #10
      ADD_STORE_ITEM + "g_bull" #0 #0 #0 ~IDENTIFIED~ #10
    BUT_ONLY
  END
END


//remove weight and maximize stacks
ACTION_DEFINE_ASSOCIATIVE_ARRAY item_type BEGIN
  arrow => 0x5
  bullet => 0xe
  dagger => 0x10
  hammer => 0x15
  dart => 0x18
  axe => 0x19
  bolt => 0x1f
END

COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
  READ_SHORT 0x38 stack
  PATCH_IF stack > 1 BEGIN
    READ_SHORT 0x1c type
    match = 0
    PHP_EACH item_type AS item => itype BEGIN
      PATCH_IF type == itype BEGIN
        GET_OFFSET_ARRAY h_array ITM_V10_HEADERS
        PHP_EACH h_array AS int => h_off BEGIN
          READ_BYTE h_off attack_type
          PATCH_IF attack_type == 2 BEGIN //ranged
            match = 1 //ranged weapon which stacks
          END
        END
      END
    END
    PATCH_IF match == 1 BEGIN
      WRITE_SHORT 0x38 65535 //max stack
      WRITE_LONG 0x4c 0 //weight
    END
  END
BUT_ONLY
