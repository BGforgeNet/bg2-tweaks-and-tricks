//when asked to hide into pack, stay there until released
//once released, can go in/out of pack as directed before

OUTER_SPRINT increase_speed_tmp ~ApplySpellRES("G_FSPED2",Myself)~

OUTER_SPRINT JumpToPack ~
IF
  Global("g_FamJumpToPack","GLOBAL",1)
  OR(2)
    !CombatCounterLT(1)
    See([EVILCUTOFF])
  !Dead("%death_var%")
THEN
  RESPONSE #100
    MoveToObject(Player1)
    GiveItemCreate("%fam_item%",Player1,0,0,0)
    DestroySelf()
END
~

OUTER_SPRINT JumpToPackInjured ~
IF
  Global("g_FamJumpToPack","GLOBAL",2)
  HPPercentLT(Myself,70)
  OR(2)
    !CombatCounterLT(1)
    See([EVILCUTOFF])
  !Dead("%death_var%")
THEN
  RESPONSE #100
    DisplayStringHead(Myself,@603) //Ouch!
    %increase_speed_tmp%
    MoveToObject(Player1)
    GiveItemCreate("%fam_item%",Player1,0,0,0)
    DestroySelf()
END
~

OUTER_SPRINT JumpToPackBadlyInjured ~
IF
  Global("g_FamJumpToPack","GLOBAL",3)
  HPPercentLT(Myself,30)
  OR(2)
    !CombatCounterLT(1)
    See([EVILCUTOFF])
  !Dead("%death_var%")
THEN
  RESPONSE #100
    DisplayStringHead(Myself,@603) //Ouch!
    %increase_speed_tmp%
    MoveToObject(Player1)
    GiveItemCreate("%fam_item%",Player1,0,0,0)
    DestroySelf()
END
~

OUTER_SPRINT HurtNotif ~
IF
  Global("g_FamHurtNotif","GLOBAL",1)
  Global("g_FamHurtNotifDone","GLOBAL",0)
  HPPercentLT(Myself,70)
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  !Dead("%death_var%")
THEN
  RESPONSE #100
    DisplayStringHead(Myself,@601)
    SetGlobal("g_FamHurtNotifDone","GLOBAL",1)
    Continue()
END
~

OUTER_SPRINT BadlyHurtNotif ~
IF
  Global("g_FamBadlyHurtNotif","GLOBAL",1)
  Global("g_FamHurtNotifDone","GLOBAL",0)
  HPPercentLT(Myself,30)
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  !Dead("%death_var%")
THEN
  RESPONSE #100
    DisplayStringHead(Myself,@602)
    SetGlobal("g_FamHurtNotifDone","GLOBAL",1)
    Continue()
END
~

OUTER_SPRINT ResetHurtNotifDone ~ //at start of each combat
IF
  Global("g_FamHurtNotifDone","GLOBAL",1)
  !CombatCounterLT(1)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    SetGlobal("g_FamHurtNotifDone","GLOBAL",0)
    Continue()
END
~

OUTER_SPRINT HitAndRun ~
IF
  Global("g_FamHitAndRun","GLOBAL",1)
  !Dead("%death_var%")
  AttackedBy([EVILCUTOFF],DEFAULT)
THEN
  RESPONSE #100
    %increase_speed_tmp%
    RunAwayFrom(LastAttackerOf(Myself),45)
    AttackOneRound(LastTargetedBy(Myself))
//    Continue()
END
/*
IF
  Global("g_FamHitAndRun","GLOBAL",1)
  !Dead("%death_var%")
  ActionListEmpty()
THEN
  RESPONSE #100
    AttackOneRound(LastTargetedBy(Myself))
//    Continue()
END
*/
~

OUTER_SPRINT FollowMaster ~
IF
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  !Detect(Player1)
  Global("g_FamFollowMaster","GLOBAL",1)
  !Dead("%death_var%")
THEN
  RESPONSE #100
//    SetInterrupt(FALSE)
    MoveToObject(Player1)
//    SetInterrupt(TRUE)
    Continue()
END
~

OUTER_SPRINT FollowMasterSee ~
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  !Range(Player1,5)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    MoveToObjectFollow(Player1)
    Continue()
END
~

OUTER_SPRINT FollowMasterBetweenAreas ~
IF
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  !InMyArea(Player1)
  Global("g_FamFollowMaster","GLOBAL",1)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    MoveGlobalObject(Myself,Player1)
    Continue()
END
~

OUTER_SPRINT CheckInventoryFull ~
//Don't pickup anything if player's inventory is full
//NOTE: Cuts off the rest of the script
IF
  HasItemSlot(Player1,SLOT_MISC18)
  HasItemSlot(Player1,SLOT_MISC17)
  HasItemSlot(Player1,SLOT_MISC16)
  HasItemSlot(Player1,SLOT_MISC15)
  HasItemSlot(Player1,SLOT_MISC14)
  HasItemSlot(Player1,SLOT_MISC13)
  HasItemSlot(Player1,SLOT_MISC12)
  HasItemSlot(Player1,SLOT_MISC11)
  HasItemSlot(Player1,SLOT_MISC10)
  HasItemSlot(Player1,SLOT_MISC9)
  HasItemSlot(Player1,SLOT_MISC8)
  HasItemSlot(Player1,SLOT_MISC7)
  HasItemSlot(Player1,SLOT_MISC6)
  HasItemSlot(Player1,SLOT_MISC5)
  HasItemSlot(Player1,SLOT_MISC4)
  HasItemSlot(Player1,SLOT_MISC3)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    SetGlobal("g_FamInventoryFull","GLOBAL",1)
    DisplayStringHead(Myself,@15)
END
~

OUTER_SPRINT CheckInventoryNotFull ~
IF
  Global("g_FamInventoryFull","GLOBAL",1)
  !Dead("%death_var%")
  OR(16)
    !HasItemSlot(Player1,SLOT_MISC18)
    !HasItemSlot(Player1,SLOT_MISC17)
    !HasItemSlot(Player1,SLOT_MISC16)
    !HasItemSlot(Player1,SLOT_MISC15)
    !HasItemSlot(Player1,SLOT_MISC14)
    !HasItemSlot(Player1,SLOT_MISC13)
    !HasItemSlot(Player1,SLOT_MISC12)
    !HasItemSlot(Player1,SLOT_MISC11)
    !HasItemSlot(Player1,SLOT_MISC10)
    !HasItemSlot(Player1,SLOT_MISC9)
    !HasItemSlot(Player1,SLOT_MISC8)
    !HasItemSlot(Player1,SLOT_MISC7)
    !HasItemSlot(Player1,SLOT_MISC6)
    !HasItemSlot(Player1,SLOT_MISC5)
    !HasItemSlot(Player1,SLOT_MISC4)
    !HasItemSlot(Player1,SLOT_MISC3)
THEN
  RESPONSE #100
    SetGlobal("g_FamInventoryFull","GLOBAL",0)
    Continue()
END
~

OUTER_SPRINT PickupGold ~
//Pickup gold, 10 times
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupGold","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
  %increase_speed_tmp%
  PickUpItem("misc07")
  PickUpItem("misc07")
  PickUpItem("misc07")
  PickUpItem("misc07")
  PickUpItem("misc07")
  PickUpItem("misc07")
  PickUpItem("misc07")
  PickUpItem("misc07")
  PickUpItem("misc07")
  PickUpItem("misc07")
  Continue()
END
~

OUTER_SPRINT PickupScalps ~
//Pickup scalps, 5 times
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupScalps","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    PickUpItem("misc86")
    PickUpItem("misc86")
    PickUpItem("misc86")
    PickUpItem("misc86")
    PickUpItem("misc86")
    Continue()
END
~

OUTER_SPRINT PickupJewelsCommon ~
//Pickup common jewelry, 2 times
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupJewelsCommon","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    %g_FamPickupJewelsCommon_action%
    %g_FamPickupJewelsCommon_action%
    Continue()
END
~

OUTER_SPRINT PickupPotionsCommon ~
//Pickup common potions x3
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupPotions","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    %g_FamPickupPotionsCommon_action%
    %g_FamPickupPotionsCommon_action%
    %g_FamPickupPotionsCommon_action%
    Continue()
END
~

OUTER_SPRINT PickupAmmoMagic ~
//Pickup (common) magical ammo, 2 times
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupAmmo","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    %g_FamPickupAmmoMagic_action%
    %g_FamPickupAmmoMagic_action%
    Continue()
END
~

OUTER_SPRINT PickupJewelryRare ~
//Pickup rare jewelry
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupJewelsRare","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    %g_FamPickupJewelsRare_action%
    Continue()
END
~

OUTER_SPRINT PickupPotionsRare ~
//Pickup rare potions x2
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupPotions","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    %g_FamPickupPotionsRare_action%
    %g_FamPickupPotionsRare_action%
    Continue()
END
~

OUTER_SPRINT PickupJewelsMagic ~
//Pickup magic jewelry
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupJewelsMagic","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    %g_FamPickupJewelsMagic_action%
    Continue()
END
~

OUTER_SPRINT PickupAmmoMagicRare ~
//Pickup (rare) magical ammo, 2 times
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupAmmo","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    %g_FamPickupAmmoMagicRare_action%
    %g_FamPickupAmmoMagicRare_action%
    Continue()
END
~

OUTER_SPRINT PickupJewelsMagicRare ~
//Pickup rare magic jewelry
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupJewelsMagicRare","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    %g_FamPickupJewelsMagicRare_action%
    Continue()
END
~

OUTER_SPRINT PickupScrolls ~
//Pickup scrolls
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupScrolls","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    %g_FamPickupScrolls_action%
    Continue()
END
~

OUTER_SPRINT PickupTrophy ~
IF
  ActionListEmpty()
  CombatCounterLT(1)
  !See([EVILCUTOFF])
  Detect(Player1)
  Global("g_FamPickupTrophy","GLOBAL",1)
  Global("g_FamInventoryFull","GLOBAL",0)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    %increase_speed_tmp%
    PickupItem("misc01")
    PickupItem("misc12")
    PickupItem("misc52")
    Continue()
END
~

OUTER_SPRINT AttackEnemy ~
IF
  ActionListEmpty()
  See([EVILCUTOFF])
  OR(2)
    Global("g_FamTactic","GLOBAL",1)
    Global("g_FamTactic","GLOBAL",2)
  !Dead("%death_var%")
THEN
  RESPONSE #100
    AttackReevaluate(NearestEnemyOf(Myself),30)
END
~

OUTER_SPRINT AttackEnemyCasters ~
IF
  ActionListEmpty()
  See([EVILCUTOFF])
  Global("g_FamTactic","GLOBAL",2)
  !Dead("%death_var%")
  OR(4)
    See(NearestEnemyOfType([0.GENERAL_ITEM.0.MAGE_ALL]))
    See(NearestEnemyOfType([0.GENERAL_ITEM.0.CLERIC_ALL]))
    See(NearestEnemyOfType([0.GENERAL_ITEM.0.DRUID_ALL]))
    See(NearestEnemyOfType([0.GENERAL_ITEM.0.BARD_ALL]))
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),30)
END
~

//actual script
<<<<<<<< .../tnt-inlined/%fam_script%.baf

%JumpToPack%
%JumpToPackInjured%
%JumpToPackBadlyInjured%

%HitAndRun%

%AttackEnemyCasters%
%AttackEnemy%

%ResetHurtNotifDone%
%HurtNotif%
%BadlyHurtNotif%

%FollowMaster%

%CheckInventoryNotFull%

%CheckInventoryFull%
%PickupGold%

%CheckInventoryFull%
%PickupScalps%

%CheckInventoryFull%
%PickupJewelsCommon%

%CheckInventoryFull%
%PickupPotionsCommon%

%CheckInventoryFull%
%PickupAmmoMagic%

%CheckInventoryFull%
%PickupTrophy%

%CheckInventoryFull%
%PickupJewelryRare%

%CheckInventoryFull%
%PickupPotionsRare%

%CheckInventoryFull%
%PickupJewelsMagic%

%CheckInventoryFull%
%PickupAmmoMagicRare%

%CheckInventoryFull%
%PickupJewelsMagicRare%

%CheckInventoryFull%
%PickupScrolls%

%FollowMaster%

%FollowMasterBetweenAreas%

>>>>>>>>
