INCLUDE ~%lib_dir%/tobex.tpa~

OUTER_SPRINT comp_dir ~%components%/smarter_familiars~
OUTER_SPRINT NewLine ~
~

// Sets variables: prefix, familiar_list, familiar_dlg
// Allows compatibility with WTP familiars
INCLUDE ~%lib_dir%/familiars.tpa~

ACTION_PHP_EACH familiar_list AS cre => item BEGIN
  OUTER_SPRINT fam_cre ~%prefix%%cre%~
  OUTER_SPRINT fam_item ~%prefix%%item%~
  OUTER_SPRINT fam_script ~g_f%item%~  // script name is limited to 8 chars
  ACTION_IF FILE_EXISTS_IN_GAME ~%fam_cre%.cre~ BEGIN
    COPY_EXISTING ~%fam_cre%.cre~ ~override~
      READ_ASCII CRE_script_name death_var
    BUT_ONLY
    COPY_EXISTING ~%fam_cre%.cre~ ~override~
      READ_ASCII SCRIPT_OVERRIDE over_script
      PATCH_IF FILE_EXISTS_IN_GAME ~%over_script%.bcs~ BEGIN  // Use existing script if possible
        over_exists = 1
      END ELSE BEGIN  // Or create a new one
        over_exists = 0
        WRITE_ASCIIE SCRIPT_OVERRIDE ~%fam_script%~ #8
      END
    BUT_ONLY

    // Actually create new script
    ACTION_IF over_exists == 0 BEGIN
      COPY ~%comp_dir%/empty.bcs~ ~override/%fam_script%.bcs~
      OUTER_SPRINT over_script ~%fam_script%~
    END

    // Apply main familiar script
    PRINT ~extending %over_script%.bcs~
    // Needs variables: fam_item, death_var
    EXTEND_TOP ~%over_script%.bcs~ ~%comp_dir%/g_fam.baf~ EVAL

    // Disable WTP leaving when attacked, that's just annoying.
    ACTION_IF ~%prefix%~ STR_EQ ~wtp~ BEGIN
      COPY_EXISTING ~%over_script%.bcs~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~AttackedBy(Player1,DEFAULT)~ ~False()~
        END
      BUT_ONLY
    END

  END

  // Auto jump out of backpack
  // WTP familiars maintain their scripts throughout the trilogy
  ACTION_IF ~%prefix%~ STR_EQ ~wtp~ BEGIN
    ACTION_FOR_EACH script IN ~baldur.bcs~ ~baldur25.bcs~ BEGIN
      // Jump out
      EXTEND_BOTTOM ~%script%~ ~%comp_dir%/jumpout.baf~ EVAL
      // Disable WTP nagging, also annoying
      COPY_EXISTING ~%script%~ ~override~
        DECOMPILE_AND_PATCH BEGIN
          REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~GlobalTimerExpired("#GWANOUT","GLOBAL")~ ~False()~
        END
      BUT_ONLY
    END
  END
  // Normal familiars have different scripts in SoA vs ToB
  ELSE BEGIN
    // Only non-*25 familiars in SoA baldur.bcs
    ACTION_IF (~%fam_cre%~ STRING_CONTAINS_REGEXP ~25~ = 1) BEGIN
      EXTEND_BOTTOM ~baldur.bcs~ ~%comp_dir%/jumpout.baf~ EVAL
    END
    // Only *25 familiars in ToB baldur25.bcs
    ACTION_IF (GAME_INCLUDES ~tob~ AND (~%fam_cre%~ STRING_CONTAINS_REGEXP ~25~ = 0)) BEGIN
      EXTEND_BOTTOM ~baldur25.bcs~ ~%comp_dir%/jumpout.baf~ EVAL
    END
  END
END

// Speed spells
// Set speed to normal. Used in dialog.
COPY ~%comp_dir%/g_fsped0.spl~ ~override~
// Permanent higher speed, to keep up with PC in Boots of Speed. Used in dialog.
COPY ~%comp_dir%/g_fsped1.spl~ ~override~
// Temporary sprint for hiding in backpack and looting. Used in familiar script.
COPY ~%comp_dir%/g_fsped2.spl~ ~override~

// New dialog options
INCLUDE ~%comp_dir%/dialogs.tpa~
