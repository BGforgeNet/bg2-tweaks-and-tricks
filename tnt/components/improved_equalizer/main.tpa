// thac0, damage bonuses for the wielder
OUTER_SET probability1 = 99
OUTER_SET target = TARGET_EFF_self
OUTER_SET timing = TIMING_while_equipped

ACTION_DEFINE_ASSOCIATIVE_ARRAY wielder BEGIN
  MASK_GENEUTRAL => 1
  MASK_LCNEUTRAL => 1
END
LAF CREATE_EFFECT
  INT_VAR
    opcode = OPCODE_to_hit_mod
    parameter1 = 2 // amount
    probability1 timing target
  STR_VAR effect = ~g_eqwt~
END
LAF CREATE_EFFECT
  INT_VAR
    opcode = OPCODE_extra_damage_mod
    parameter1 = 2 // amount
    probability1 timing target
  STR_VAR effect = ~g_eqwd~
END

ACTION_DEFINE_ASSOCIATIVE_ARRAY enemy BEGIN
  LAWFUL_NEUTRAL, ln => 2
  CHAOTIC_NEUTRAL, cn => 2
  NEUTRAL_GOOD, ng => 2
  NEUTRAL_EVIL, ne => 2
  LAWFUL_GOOD, lg => 4
  LAWFUL_EVIL, le => 4
  CHAOTIC_GOOD, cg => 4
  CHAOTIC_EVIL, ce => 4
END
OUTER_SET parameter2 = IDS_align
ACTION_PHP_EACH enemy AS align_slug => bonus BEGIN
  OUTER_SPRINT align $align_slug(0)
  OUTER_SPRINT slug $align_slug(1)
  LAF CREATE_EFFECT
    INT_VAR
      opcode = OPCODE_thac0_vs_creature_type_mod
      parameter1 = IDS_OF_SYMBOL (~align~ ~%align%~)
      probability1 timing target parameter2
      parameter3 = bonus
    STR_VAR effect = ~g_eqtt%slug%~
  END
  ACTION_IF NOT FILE_EXISTS_IN_GAME ~g_eqtd%bonus%.eff~ BEGIN
    LAF CREATE_EFFECT
      INT_VAR
        opcode = OPCODE_hp_damage
        parameter1 = bonus
        parameter2 = DAMAGETYPE_slashing
        probability1
        timing = TIMING_instant
        target = TARGET_EFF_preset
      STR_VAR effect = ~g_eqtd%bonus%~
    END
  END
END

// blindness and confusion
OUTER_SPRINT law_chaos_spell ~g_eq_lc~
OUTER_SPRINT good_evil_spell ~g_eq_ge~
OUTER_SET duration = 12

ACTION_FOR_EACH spell IN ~%law_chaos_spell%~ ~%good_evil_spell%~ BEGIN
  LAF CREATE_EFFECT
    INT_VAR
      probability1 timing
      opcode = OPCODE_cast_spell_at_creature
      parameter2 = CASTING_TYPE_instant
      target = TARGET_EFF_preset
    STR_VAR
      resource = ~%spell%~
      effect = ~%spell%~
  END
END

LAF CREATE_SHELL_SPELL INT_VAR duration STR_VAR spell = ~%law_chaos_spell%~ type = blindness END
LAF CREATE_SHELL_SPELL INT_VAR duration STR_VAR spell = ~%good_evil_spell%~ type = confusion END

COPY_EXISTING ~sw1h54.itm~ ~override~
  SAY IDENTIFIED_DESC @1
  WRITE_LONG ITM_enchantment 4

  // fixpack original bonuses
  LPF DELETE_EFFECT INT_VAR match_opcode = OPCODE_use_eff_file END

  // wielder bonuses
  opcode = OPCODE_use_eff_file
  timing = TIMING_while_equipped
  parameter2 = EFFECT_IDS_align
  target = TARGET_EFF_self
  PHP_EACH wielder AS align => int BEGIN
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode timing parameter2 probability1 target
        parameter1 = IDS_OF_SYMBOL (~align~ ~%align%~)
      STR_VAR resource = "g_eqwt"
    END
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode timing parameter2 probability1 target
        parameter1 = IDS_OF_SYMBOL (~align~ ~%align%~)
      STR_VAR resource = "g_eqwd"
    END
  END


  // target type bonuses
  PHP_EACH enemy AS align_slug => bonus BEGIN
    SPRINT align $align_slug(0)
    SPRINT slug $align_slug(1)
    LPF ADD_ITEM_EQEFFECT // thac0 bonus is global
      INT_VAR
        opcode timing parameter2 probability1 target
        parameter1 = IDS_OF_SYMBOL (~ea~ ~anyone~)
        parameter2 = IDS_ea
      STR_VAR resource = "g_eqtt%slug%"
    END
    LPF ADD_ITEM_EFFECT // damage is on hit only
      INT_VAR
        opcode timing parameter2 probability1
        target = TARGET_EFF_preset
        parameter1 = IDS_OF_SYMBOL (~align~ ~%align%~)
        parameter2 = IDS_align
        header = 1 type = 1 // melee
      STR_VAR resource = EVAL "g_eqtd%bonus%"
    END
  END

  // custom effects on hit
  DEFINE_ASSOCIATIVE_ARRAY spell BEGIN
    MASK_GOOD, FLAG_SAVINGTHROW_spell => ~%good_evil_spell%~
    MASK_EVIL, FLAG_SAVINGTHROW_spell  => ~%good_evil_spell%~
    MASK_LAWFUL, FLAG_SAVINGTHROW_breath  => ~%law_chaos_spell%~
    MASK_CHAOTIC, FLAG_SAVINGTHROW_breath => ~%law_chaos_spell%~
  END

  PHP_EACH spell AS align_sthrow => spell BEGIN
    SPRINT align $align_sthrow(0)
    SPRINT sthrow $align_sthrow(1)
    sthrow = EVAL ~%%sthrow%%~
    LPF ADD_ITEM_EFFECT
      INT_VAR
        opcode = OPCODE_use_eff_file
        target = TARGET_EFF_preset
        timing = TIMING_instant
        parameter1 = IDS_OF_SYMBOL (~align~ ~%align%~)
        parameter2 = EFFECT_IDS_align
        savingthrow = ~%sthrow%~
        header = 1 type = 1 // melee
        probability1 = 100
      STR_VAR resource = ~%spell%~
    END
  END

  // know alightnment ability
  READ_ASCII SPL_icon icon
  LPF ADD_ITEM_HEADER
    INT_VAR
      charges = 3
      depletion = DEPLETION_recharges
      flags = FLAG_HEADER_recharge_after_rest
      target = TARGET_HEADER_creature
      range = 30
    STR_VAR
      icon = ~%icon%~
  END 
  LPF ADD_ITEM_EFFECT
    INT_VAR
      opcode = OPCODE_cast_spell_at_creature
      parameter1 = 10 // default casting level from items
      parameter2 = CASTING_TYPE_instant
      timing = TIMING_instant
      header = 2
      target = TARGET_EFF_preset
    STR_VAR
      resource = ~%WIZARD_KNOW_ALIGNMENT%~
  END

BUT_ONLY
