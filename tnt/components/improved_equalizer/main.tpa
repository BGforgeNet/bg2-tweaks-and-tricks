// thac0, damage bonuses
OUTER_SET probability1 = 99
OUTER_SET target = TARGET_FEATURE_self
OUTER_SET timing = TIMING_while_equipped
OUTER_SET parameter1 = i // amount
ACTION_FOR_EACH i IN 1 2 3 4 BEGIN
  LAF CREATE_EFFECT
    INT_VAR
      probability1 target timing parameter1
      opcode = OPCODE_thac0_mod
    STR_VAR
      effect = EVAL ~g_equat%i%~ //thac0
  END
  LAF CREATE_EFFECT
    INT_VAR
      probability1 target timing parameter1
      opcode = OPCODE_extra_damage_mod
    STR_VAR
      effect = EVAL ~g_equad%i%~ //damage
  END
END

COPY_EXISTING ~sw1h54.itm~ ~override~
  SAY DESC @1

  opcode = OPCODE_use_eff_file
  target = TARGET_FEATURE_self
  timing = TIMING_while_equipped
  parameter2 = EFFECT_IDS_align

  DEFINE_ASSOCIATIVE_ARRAY align_effnum BEGIN
    NEUTRAL => 4 // True Neutral
    LAWFUL_NEUTRAL => 3
    CHAOTIC_NEUTRAL => 3
    NEUTRAL_GOOD => 2
    NEUTRAL_EVIL => 2
    CHAOTIC_GOOD => 1
    LAWFUL_EVIL => 1
//    LAWFUL_GOOD => 0
//    CHAOTIC_EVIL => 0
  END
  PHP_EACH align_effnum AS align => i BEGIN
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode target timing parameter2 probability1
        parameter1 = IDS_OF_SYMBOL (~align~ ~%align%~)
      STR_VAR resource = "g_equat%i%"
    END
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode target timing parameter2 probability1
        parameter1 = IDS_OF_SYMBOL (~align~ ~%align%~)
      STR_VAR resource = "g_equad%i%"
    END
  END

  WRITE_LONG ITM_enchantment 4

  // know alightnment ability
  READ_ASCII SPL_icon icon
  LPF ADD_ITEM_HEADER
    INT_VAR
      insert_point = 0
      charges = 3
      depletion = DEPLETION_recharges
      flags = FLAG_HEADER_recharge_after_rest
    STR_VAR
      icon = ~%icon%~
  END 
  LPF ADD_ITEM_EFFECT
    INT_VAR
      opcode = OPCODE_cast_spell_at_creature
      parameter1 = 10 // default casting level from items
      paremeter2 = 1 // cast instantly
      timing = TIMING_permanent
    STR_VAR
      resource = WIZARD_KNOW_ALIGNMENT
  END

BUT_ONLY
