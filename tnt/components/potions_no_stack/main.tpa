INCLUDE ~%components%/potions_no_stack/macro.tpa~

PRINT ~Patching potions...~
OUTER_SPRINT ~refresh_strref~ @1
ACTION_FOR_EACH item IN
  potn02
  potn22
  potn09
  potn41
  potn18
  potn29
  potn31
  potn40
  potn34
  potn35
  potn36
  potn37
  potn39
  potn42
  potn46
  potn15
BEGIN
  OUTER_SPRINT ~item_to_unstack~ ~%item%~
  OUTER_SPRINT ~spellname~ ~g_%item%~
  LAM ~UNSTACK_ITEM~
END
//RR compatibility, remove double "Gulp"
ACTION_FOR_EACH file IN ~rr#pnp36.spl~ ~rr#pnp39.spl~ BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%file%~ BEGIN
    COPY_EXISTING ~%file%~ ~override~
      LPF DELETE_SPELL_EFFECT
        INT_VAR
          opcode_to_delete = OPCODE_text_display_string
      END
    BUT_ONLY
  END
END

PRINT ~Patching scrolls...~
ACTION_DEFINE_ARRAY scrolls BEGIN
  SCROLL_protection_from_acid
  SCROLL_protection_from_cold
  SCROLL_protection_from_electricity
  SCROLL_protection_from_fire
END
OUTER_SPRINT ~refresh_strref~ @2
ACTION_PHP_EACH scrolls AS int => item BEGIN
  OUTER_SPRINT item EVAL ~%%item%%~
  OUTER_SPRINT ~item_to_unstack~ ~%item%~
  OUTER_SPRINT ~spellname~ ~g_%item%~
  LAM ~UNSTACK_ITEM~
END
