LAF list_web_spells RET_ARRAY web_spells = spells END

COPY_EXISTING ~clck22.itm~  ~override~
  SAY DESC @1
  LPF ADD_ITEM_EQEFFECT
    INT_VAR
      opcode = OPCODE_protection_from_opcode
      target = TARGET_HEADER_self
      timing = TIMING_while_equipped
      parameter2 = OPCODE_web_effect
      resist_dispel = DISPELLABLE | IGNORE_MAGIC_RESISTANCE
  END
  LPF ADD_ITEM_EQEFFECT
    INT_VAR
      opcode = OPCODE_prevent_special_effect_icon
      target = TARGET_HEADER_self
      timing = TIMING_while_equipped
      parameter2 = ICON_webbed
      resist_dispel = DISPELLABLE | IGNORE_MAGIC_RESISTANCE
  END

  // extra protection from individual spells
  PHP_EACH web_spells AS index => spell BEGIN
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode = OPCODE_protection_from_spell
        target = TARGET_HEADER_self
        timing = TIMING_while_equipped
        resist_dispel = DISPELLABLE | IGNORE_MAGIC_RESISTANCE
      STR_VAR
        resource = EVAL ~%spell%~
    END
  END

  // save vs poison bonus
  LPF ADD_ITEM_EQEFFECT
    INT_VAR
      opcode = OPCODE_save_vs_death_mod
      target = TARGET_HEADER_self
      timing = TIMING_while_equipped
      paremeter2 = 2 //amount
      resist_dispel = DISPELLABLE | IGNORE_MAGIC_RESISTANCE
  END

  // web spell ability
  READ_ASCII SPL_icon icon
  LPF ADD_ITEM_HEADER
    INT_VAR
      range = 60 // I think 60 is correct?
      insert_point = 0
      charges = 1
      depletion = DEPLETION_recharges
      flags = FLAG_HEADER_recharge_after_rest
    STR_VAR
      icon = ~%icon%~
  END 

  // web spell
  LPF ADD_ITEM_EFFECT
    INT_VAR
      opcode = OPCODE_cast_spell_at_point
      parameter1 = 10 // default casting level from items
      paremeter2 = 1 // cast instantly
      timing = TIMING_permanent
    STR_VAR
      resource = WIZARD_WEB
  END

BUT_ONLY
