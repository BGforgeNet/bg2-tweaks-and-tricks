// normal detection
IF
  Detected([ANYONE])
  %is_active%
THEN
  RESPONSE #100
    SetGlobal("%trap_var_base%_detected", "MYAREA", 1)
    DisplayStringHead(Myself, ~%trap_warning_string%~)
    SetGlobalTimer("%trap_var_base%_timer", "MYAREA", %notification_delay%)
    Continue()
END

// instant detection
IF
  %traps_baf_instant_toggle%
  Global("%trap_var_base%_detected", "MYAREA", 0)
  %is_active%
  %traps_baf_class%
  %traps_baf_range%
  !StateCheck(LastSeenBy, CD_STATE_NOTVALID)
  !CheckStatLT(LastSeenBy, %detection%, TRAPS)
  TriggerOverride(LastSeenBy, ModalState(DETECTTRAPS))
  ActionListEmpty()
THEN
	RESPONSE #1
    ReallyForceSpellRES("%trap_spl%", Myself)
    SetGlobal("%trap_var_base%_detected", "MYAREA", 1)
    Continue()
END


// Display detected trap type second and consequtive times
IF
  Global("%trap_var_base%_detected", "MYAREA", 1) // has been detected with thief's skill
  Global("%trap_var_base%_disarmed", "MYAREA", 0) // hasn't been disarmed or set off
  !GlobalTimerNotExpired("%trap_var_base%_timer", "MYAREA") // don't spam the dialog box
  %is_active%
  %traps_baf_combat%
  Detect([PC])
  %traps_baf_range%
  ActionListEmpty()
THEN
  RESPONSE #100
    SetGlobalTimer("%trap_var_base%_timer", "MYAREA", %notification_delay%)
    DisplayStringHead(Myself, ~%trap_warning_string%~)
    Continue()
END

//main detection check
IF
  Global("%trap_var_base%_detected", "MYAREA", 0)
  Global("%trap_var_base%_disarmed", "MYAREA", 0)
  !GlobalTimerNotExpired("%trap_var_base%_timer", "MYAREA") // don't spam the dialog box
  !GlobalTimerNotExpired("%pause_timer%", "GLOBAL") // don't spam pause
  %is_active%
  %traps_baf_combat%
  Detect([PC])
  %traps_baf_range%
  !StateCheck(LastSeenBy, CD_STATE_NOTVALID)
  ActionListEmpty()
THEN
  RESPONSE #1
    SetGlobal("%trap_var_base%_string", "MYAREA", 1)
    SetGlobalTimer("%trap_var_base%_timer", "MYAREA", %notification_delay%)
    SetGlobalTimer("%pause_timer%", "GLOBAL", %notification_delay%)
    %traps_baf_stop%
    %traps_baf_sound%
    %traps_baf_view%
    %traps_baf_pause%
    Continue()
END

// intuition: action find traps
IF
  Global("%trap_var_base%_detected", "MYAREA", 0)
  %is_active%
  %traps_baf_class%
  %traps_baf_range%
  !StateCheck(LastSeenBy, CD_STATE_NOTVALID)
  !TriggerOverride(LastSeenBy, ModalState(DETECTTRAPS))
THEN
	RESPONSE #1
    ActionOverride(LastSeenBy, FindTraps())
    Continue()
END

// shorter random string display
IF
  Global("%trap_var_base%_string", "MYAREA", 1)
  ActionListEmpty()
THEN
  RESPONSE #1
    DisplayStringHead(LastSeenBy, @3)
    Continue()
  RESPONSE #1
    DisplayStringHead(LastSeenBy, @4)
    Continue()
  RESPONSE #1
    DisplayStringHead(LastSeenBy, @5)
    Continue()
  RESPONSE #1
    DisplayStringHead(LastSeenBy, @6)
    Continue()
  RESPONSE #1
    DisplayStringHead(LastSeenBy, @7)
    Continue()
  RESPONSE #1
    DisplayStringHead(LastSeenBy, @8)
    Continue()
  RESPONSE #1
    DisplayStringHead(LastSeenBy, @9)
    Continue()
END
IF
  Global("%trap_var_base%_string", "MYAREA", 1)
  ActionListEmpty()
THEN
  RESPONSE #1
    SetGlobal("%trap_var_base%_string", "MYAREA", 0)
    Continue()
END


//Start warning again when reset
IF
  Reset([ANYONE])
THEN
  RESPONSE #100
    SetGlobal("%trap_var_base%_disarmed", "MYAREA", 0)
    Continue()
END

//Stop warning when fired
IF
  OR(3)
    Disarmed([ANYONE])
    Opened([ANYONE])  // for containers
    Entered([ANYONE])  // for regions
THEN
  RESPONSE #100
    SetGlobal("%trap_var_base%_disarmed", "MYAREA", 1)
    Continue()
END
