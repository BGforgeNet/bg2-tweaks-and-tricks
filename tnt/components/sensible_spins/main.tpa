OUTER_TEXT_SPRINT comp_dir ~%components%/%comp_name%~

OUTER_TEXT_SPRINT spl_despin ~g_despi~ // If the key is not 8 characters long, all memorized copies will be removed
OUTER_TEXT_SPRINT icon ~%spl_despin%~
COPY ~%comp_dir%/%icon%.bam~ ~override~ //"cancel spin" spell icon
COPY ~%comp_dir%/%icon%b.bam~ ~override~ //"cancel spin" spell header icon

ADD_SECTYPE g_despin @1

// "cancel spin" ability
LAF CREATE_SPELL
  INT_VAR
    flags = FLAG_SPL_ignore_dead_wild_magic
    sectype = g_despin
  STR_VAR spell = ~%spl_despin%~
END
COPY_EXISTING ~%spl_despin%.spl~ override
  LPF ADD_SPELL_HEADER
    INT_VAR target = TARGET_HEADER_self
    STR_VAR icon=~%icon%b~
  END
  LPF ADD_SPELL_EFFECT //remove active spins
    INT_VAR
      opcode = OPCODE_remove_secondary_type
      parameter1 = 1 //max level
      parameter2 = g_despin
      timing = TIMING_permanent
      target = TARGET_EFF_self
  END
  LPF ADD_SPELL_EFFECT //remove self
    INT_VAR
      opcode = OPCODE_remove_spell
      timing = TIMING_permanent
      target = TARGET_EFF_self
    STR_VAR resource = ~%spl_despin%~
  END
  LPF ADD_SPELL_EFFECT // allow to cast another spell right away
    INT_VAR
      opcode = OPCODE_aura_cleansing
      parameter2 = 1
      timing = TIMING_duration
      duration = 6 // 1 round
      target = TARGET_EFF_self
  END
  SAY NAME1 @2

  WRITE_ASCIIT SPL_icon ~%icon%~
BUT_ONLY

COPY_EXISTING ~%BLADE_OFFENSIVE_SPIN%.spl~ override
              ~%BLADE_DEFENSIVE_SPIN%.spl~ override
  WRITE_BYTE SPL_sectype g_despin
  PATCH_FOR_EACH spl IN ~%BLADE_OFFENSIVE_SPIN%~ ~%BLADE_DEFENSIVE_SPIN%~ BEGIN
    LPF DELETE_EFFECT
      INT_VAR
        match_opcode = OPCODE_protection_from_spell
      STR_VAR
        match_resource = ~%spl%~
    END
  END
  LPF ADD_SPELL_EFFECT //remove active spins first
    INT_VAR
      opcode = OPCODE_remove_secondary_type
      parameter1 = 1 //max level
      parameter2 = g_despin
      timing = TIMING_permanent
      insert_point = 0
      target = TARGET_EFF_self
  END
  LPF ADD_SPELL_EFFECT //remove "cancel spin" ability from previous spin, if any
    INT_VAR
      opcode = OPCODE_remove_spell
      timing = TIMING_permanent
      target = TARGET_EFF_self
      insert_point = 1
    STR_VAR resource = ~%spl_despin%~
  END
  LPF ADD_SPELL_EFFECT //add "cancel spin" ability
    INT_VAR
      opcode = OPCODE_give_ability
      timing = TIMING_permanent
      target = TARGET_EFF_self
    STR_VAR resource = ~%spl_despin%~
  END
  LPF ADD_SPELL_EFFECT //remove "cancel spin" ability on expiration
    INT_VAR
      opcode = OPCODE_remove_spell
      timing = TIMING_delayed
      duration = 24
      target = TARGET_EFF_self
    STR_VAR resource = ~%spl_despin%~
  END
BUT_ONLY
