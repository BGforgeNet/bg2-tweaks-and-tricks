OUTER_TEXT_SPRINT comp_dir ~%components%/%comp_name%~

OUTER_TEXT_SPRINT spl_despin ~g_despin~
OUTER_TEXT_SPRINT icon ~%spl_despin%~
COPY ~%comp_dir%/%icon%.bam~ ~override~ //"cancel spin" ability icon

OUTER_TEXT_SPRINT sectype ~g_spin~
ADD_SECTYPE ~%sectype%~ @1
ADD_SECTYPE ~%sectype%~ @1 // set sectypeName to id
OUTER_SET sectype_id = sectypeName  //set by second ADD_SECTYPE

// "cancel spin" ability
LAF CREATE_SPELL
  INT_VAR
    flags = FLAG_SPL_ignore_dead_wild_magic
    sectype = sectype_id
  STR_VAR spell = ~%spl_despin%~
END
COPY_EXISTING ~%spl_despin%.spl~ override
  LPF ADD_SPELL_HEADER END
  LPF ADD_SPELL_EFFECT //remove active spins
    INT_VAR
      opcode = OPCODE_remove_secondary_type
      parameter1 = 1 //max level
      paremeter2 = sectype_id
      timing = TIMING_permanent
      target = TARGET_FEATURE_self
  END
  LPF ADD_SPELL_EFFECT //remove self
    INT_VAR
      opcode = OPCODE_remove_spell
      timing = TIMING_permanent
      target = TARGET_FEATURE_self
    STR_VAR resource = ~%spl_despin%~
  END
  LPF ADD_SPELL_EFFECT // allow to cast another spell right away
  INT_VAR
    opcode = OPCODE_aura_cleansing
    paremeter2 = 1
    timing = TIMING_duration
    duration = 6 // 1 round
    target = TARGET_FEATURE_self
END

  WRITE_ASCIIT SPL_icon ~%icon%~
BUT_ONLY

ACTION_FOR_EACH spl IN ~%BLADE_OFFENSIVE_SPIN%~ ~%BLADE_DEFENSIVE_SPIN%~ BEGIN //spins
  COPY_EXISTING ~%spl%.spl~ override
    WRITE_BYTE SPL_sectype sectype_id
    LPF ADD_SPELL_EFFECT //remove active spins first
      INT_VAR
        opcode = OPCODE_remove_secondary_type
        parameter1 = 1 //max level
        paremeter2 = sectype_id
        timing = TIMING_permanent
        insert_point = 0
        target = TARGET_FEATURE_self
    END
    LPF ADD_SPELL_EFFECT //add "cancel spin" ability
      INT_VAR
        opcode = OPCODE_give_ability
        timing = TIMING_permanent
        target = TARGET_FEATURE_self
      STR_VAR resource = ~%spl_despin%~
    END
    LPF ADD_SPELL_EFFECT //remove "cancel spin" ability
      INT_VAR
        opcode = OPCODE_remove_spell
        timing = TIMING_delayed
        duration = 24
        target = TARGET_FEATURE_self
      STR_VAR resource = ~%spl_despin%~
    END
  BUT_ONLY
END
