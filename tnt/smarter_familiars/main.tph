OUTER_SPRINT NewLine ~
~
OUTER_SPRINT g_FamPickupJewels_action ~~
OUTER_SPRINT g_FamPickupJewelsCommon_action ~~
OUTER_SPRINT g_FamPickupJewelsRare_action ~~
OUTER_SPRINT g_FamPickupJewelsMagic_action ~~
OUTER_SPRINT g_FamPickupJewelsMagicRare_action ~~
OUTER_SPRINT g_FamPickupAmmoMagic_action ~~
OUTER_SPRINT g_FamPickupAmmoMagicRare_action ~~
OUTER_SPRINT g_FamPickupPotionsCommon_action ~~
OUTER_SPRINT g_FamPickupPotionsRare_action ~~
OUTER_SPRINT g_FamPickupScrolls_action ~~

INCLUDE ~%MOD_FOLDER%/smarter_familiars/autoloot_lists.tph~

COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
  PATCH_IF (SOURCE_SIZE >= 114) BEGIN
  READ_ASCII 0x3a invicon ELSE ~No File~  (8) NULL  //8 (resref) Inventory icon
  READ_ASCII 0x44 groundicon ELSE ~No File~(8) NULL //8 (resref) Ground icon
  READ_ASCII 0x58 descicon ELSE ~No File~  (8) NULL //8 (resref) Description icon
    //following should weed out many player unusable items
    PATCH_IF ( (FILE_EXISTS_IN_GAME ~%invicon%.bam~)
           AND (FILE_EXISTS_IN_GAME ~%groundicon%.bam~)
           AND (FILE_EXISTS_IN_GAME ~%descicon%.bam~) ) BEGIN
      READ_BYTE 0x18 flags
      READ_SHORT 0x1c item_type
      PATCH_IF ((flags BAND 0b00000100) = 0b00000100)  // droppable
           AND ((flags BAND 0b00001000) = 0b00001000) BEGIN // displayable in shop

      //Gems
      PATCH_IF (item_type = 0x22) BEGIN
        PATCH_IF (VARIABLE_IS_SET $g_GemsCommon(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupJewelsCommon_action ~%g_FamPickupJewelsCommon_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_GemsRare(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupJewelsRare_action ~%g_FamPickupJewelsRare_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
      END
      //Rings
      PATCH_IF (item_type = 0xa) BEGIN
        PATCH_IF (VARIABLE_IS_SET $g_RingsCommon(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupJewelsCommon_action ~%g_FamPickupJewelsCommon_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_RingsRare(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupJewelsRare_action ~%g_FamPickupJewelsRare_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_RingsMagic(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupJewelsMagic_action ~%g_FamPickupJewelsMagic_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_RingsMagicRare(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupJewelsMagicRare_action ~%g_FamPickupJewelsMagicRare_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
      END
      //Necklaces
      PATCH_IF (item_type = 0x1) BEGIN
        PATCH_IF (VARIABLE_IS_SET $g_NecklacesCommon(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupJewelsCommon_action ~%g_FamPickupJewelsCommon_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_NecklacesRare(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupJewelsRare_action ~%g_FamPickupJewelsRare_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_NecklacesMagic(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupJewelsMagic_action ~%g_FamPickupJewelsMagic_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_NecklacesMagicRare(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupJewelsMagicRare_action ~%g_FamPickupJewelsMagicRare_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
      END

      //Ammo: arrows
      PATCH_IF (item_type = 0x5) BEGIN
        PATCH_IF (VARIABLE_IS_SET $g_ArrowsMagic(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupAmmoMagic_action ~%g_FamPickupAmmoMagic_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_ArrowsMagicRare(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupAmmoMagicRare_action ~%g_FamPickupAmmoMagicRare_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
      END
      //Ammo: bolts
      PATCH_IF (item_type = 0x1f) BEGIN
        PATCH_IF (VARIABLE_IS_SET $g_BoltsMagic(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupAmmoMagic_action ~%g_FamPickupAmmoMagic_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_BotlsMagicRare(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupAmmoMagicRare_action ~%g_FamPickupAmmoMagicRare_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
      END
      //Ammo: darts
      PATCH_IF (item_type = 0x18) BEGIN
        PATCH_IF (VARIABLE_IS_SET $g_DartsMagic(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupAmmoMagic_action ~%g_FamPickupAmmoMagic_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_DartsMagicRare(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupAmmoMagicRare_action ~%g_FamPickupAmmoMagicRare_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
      END
      //Ammo: bullets
      PATCH_IF (item_type = 0xe) BEGIN
        PATCH_IF (VARIABLE_IS_SET $g_BulletsMagic(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupAmmoMagic_action ~%g_FamPickupAmmoMagic_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_BulletsMagicRare(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupAmmoMagicRare_action ~%g_FamPickupAmmoMagicRare_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
      END

      //Potions
      PATCH_IF (item_type = 0x9) BEGIN
        PATCH_IF (VARIABLE_IS_SET $g_PotionsCommon(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupPotionsCommon_action ~%g_FamPickupPotionsCommon_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
        PATCH_IF (VARIABLE_IS_SET $g_PotionsRare(~%SOURCE_RES%~)) BEGIN
          SPRINT g_FamPickupPotionsRare_action ~%g_FamPickupPotionsRare_action%%NewLine%PickUpItem("%SOURCE_RES%")~
        END
      END

      //Scrolls
      PATCH_IF (item_type = 0xb AND (VARIABLE_IS_SET $g_Scrolls(~%SOURCE_RES%~))) BEGIN
        SPRINT g_FamPickupScrolls_action ~%g_FamPickupScrolls_action%%NewLine%PickUpItem("%SOURCE_RES%")~
      END
    END
  END
END
BUT_ONLY
//Daggers: there only one type of throwing daggers worth picking up: poisoned throwing daggers
OUTER_SPRINT g_FamPickupAmmoRare_action ~%g_FamPickupAmmoRare_action%%NewLine%PickUpItem("DAGG16")~

//Script to jump out of the pack, added to baldur.bcs
<<<<<<<< .../tnt-inlined/g_familiar_jumpout.baf
IF
  Global("g_FamiliarJumpOut","GLOBAL",1)
  Global("g_FamStayInPack","GLOBAL",0)
  HasItem("%fam_item%",Player1)
  Delay(12) //check every 2 rounds
  CombatCounterLT(1)
  !OnScreen([EVILCUTOFF])
THEN
  RESPONSE #100
    TakePartyItem("%fam_item%")
    DestroyItem("%fam_item%")
    MoveGlobalObject("%fam_cre%",Player1)
    Continue()
END
>>>>>>>>

//Override script
INCLUDE ~%MOD_FOLDER%/lib/familiars.tph~ //familiar_list, compatibility with WTPfamiliars

ACTION_PHP_EACH familiar_list AS cre => item BEGIN
  OUTER_SPRINT fam_cre ~%prefix%%cre%~
  OUTER_SPRINT fam_item ~%prefix%%item%~
  ACTION_IF FILE_EXISTS_IN_GAME ~%fam_cre%.cre~ BEGIN
    COPY_EXISTING ~%fam_cre%.cre~ ~override~
      READ_ASCII 0x280 death_var
    BUT_ONLY
    INCLUDE ~%MOD_FOLDER%/smarter_familiars/g_fam.baf.tph~ //ACTION to generate script. Depends on %death_var% and %fam_item%
    COPY_EXISTING ~%fam_cre%.cre~ ~override~
      READ_ASCII 0x248 over_script
      PATCH_PRINT ~cre is %fam_cre%, script is %over_script%, death_var is %death_var%~
      PATCH_IF FILE_EXISTS_IN_GAME ~%over_script%.bcs~ BEGIN //use existing script if possible
        over_exists = 1
      END ELSE BEGIN //or create a new one
        over_exists = 0
        WRITE_ASCIIE 0x248 ~g_%fam_cre%~ #8
      END
    BUT_ONLY
    ACTION_IF over_exists = 1 BEGIN
      EXTEND_TOP ~%over_script%.bcs~ ~.../tnt-inlined/g_%fam_cre%.baf~ EVAL
    END ELSE BEGIN
      COMPILE EVAL ~.../tnt-inlined/g_%fam_cre%.baf~
    END
  END
  ACTION_IF ~%prefix%~ STR_EQ ~wtp~ BEGIN //WTP fams
    EXTEND_BOTTOM ~baldur.bcs~ ~.../tnt-inlined/g_familiar_jumpout.baf~ EVAL
    EXTEND_BOTTOM ~baldur25.bcs~ ~.../tnt-inlined/g_familiar_jumpout.baf~ EVAL
  END ELSE BEGIN //normal fams
    ACTION_IF (~%fam_cre%~ STRING_CONTAINS_REGEXP ~25~ = 1) BEGIN //only non-*25 familiars in SoA baldur.bcs
      EXTEND_BOTTOM ~baldur.bcs~ ~.../tnt-inlined/g_familiar_jumpout.baf~ EVAL
    END
    ACTION_IF (GAME_INCLUDES ~tob~ AND (~%fam_cre%~ STRING_CONTAINS_REGEXP ~25~ = 0)) BEGIN //only *25 familiars in ToB baldur25.bcs
      EXTEND_BOTTOM ~baldur25.bcs~ ~.../tnt-inlined/g_familiar_jumpout.baf~ EVAL
    END
  END
END

//New dialog options
ACTION_PHP_EACH familiar_dlg AS dialog => state BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%dialog%.dlg~ BEGIN
    COMPILE EVAL ~%MOD_FOLDER%/smarter_familiars/familiars.d~ //auto_tra
  END
END
