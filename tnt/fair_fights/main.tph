//Borda the cheating magic merchant
ACTION_IF FILE_EXISTS_IN_GAME ~borda.cre~ BEGIN  
  COPY_EXISTING ~borda.cre~ ~override~
    WRITE_BYTE 0x005d 0 //magic resistance
  BUT_ONLY
END

//Shandalar, Ulgoth's Beard mage
ACTION_IF FILE_EXISTS_IN_GAME ~shandal2.cre~ BEGIN  
  COPY_EXISTING ~shandal2.cre~ ~override~
    //Nullify all resistances
    PATCH_FOR_EACH offset IN 0x59 0x5a 0x5b 0x5c 0x5d 0x5e 0x5f 0x60 0x61 0x62 0x63 BEGIN
      WRITE_BYTE %offset% 0
    END
    //Set human natural and effective AC
    WRITE_SHORT 0x46 10
    WRITE_SHORT 0x48 10
    //Set cleric-mage class instead of innocent
    WRITE_BYTE 0x273 14
    //Set alignment to CN
    WRITE_BYTE 0x27b 50
    //Saves
    WRITE_BYTE 0x54 2  // Save versus death (0-20)
    WRITE_BYTE 0x55 3  // Save versus wands (0-20)
    WRITE_BYTE 0x56 5  // Save versus polymorph (0-20)
    WRITE_BYTE 0x57 7  // Save versus breath attacks (0-20)
    WRITE_BYTE 0x58 4  // Save versus spells (0-20)
  BUT_ONLY
END

//Arkanis Gath (on attacking Renal)
ACTION_IF FILE_EXISTS_IN_GAME ~stdeath.cre~ BEGIN  
  COPY_EXISTING ~stdeath.cre~ ~override~
    REPLACE_CRE_ITEM ~sw1h10~ #0 #0 #0 ~identified&unstealable&undroppable~ ~weapon1~
    REMOVE_CRE_ITEM ~minhp1~
  BUT_ONLY
END

//Marchol Harpell
ACTION_IF FILE_EXISTS_IN_GAME ~c6harp.dlg~ BEGIN  
  COMPILE ~%MOD_FOLDER%/fair_fights/malchor.d~
  COPY_EXISTING ~c6harp.cre~ ~override~
    REMOVE_CRE_ITEM ~imoenhp1~
  BUT_ONLY
  COPY_EXISTING ~baldur.bcs~ ~override~
    DECOMPILE_AND_PATCH BEGIN
      REPLACE_TEXTUALLY
        ~!Exists("c6harp")~
        ~!Exists("c6harp")
!Dead("c6harp")~
    END
  BUT_ONLY
END

//Laskal, shadow druid with a "message"
ACTION_IF FILE_EXISTS_IN_GAME ~laskal.cre~ BEGIN  
  COPY_EXISTING ~laskal.cre~ ~override~
    WRITE_BYTE 0x273 0x0b //Set druid class instead of innocent
  BUT_ONLY
END

//Phandalyn, BG1 tavern paladin
ACTION_IF FILE_EXISTS_IN_GAME ~phanda.cre~ BEGIN
  COPY_EXISTING ~phanda.cre~ ~override~
    WRITE_BYTE 0x273 0x06 //Set paladin class instead of innocent
  BUT_ONLY
END

//Winski Perorate
ACTION_IF FILE_EXISTS_IN_GAME ~winski2.cre~ BEGIN
  COPY_EXISTING ~winski2.cre~ ~override~
    WRITE_BYTE 0x273 0x01 //mage class instead of innocent
  BUT_ONLY
END

//Kahrk, the mightiest ogre mage - in Firewine. And others...
COPY_EXISTING_REGEXP ~.*\.cre~ ~override~
  REMOVE_CRE_ITEM
    ~mageamul~
    ~magebrac~
    ~mage01~
    ~mage02~
    ~mage03~
    ~mage04~
    ~mage05~
    ~mage06~
    ~stonskin~
BUT_ONLY
